indexing
	description: "Objects that represent an EV_TITLED_WINDOW.%
		%The original version of this class was generated by EiffelBuild."
	legal: "See notice at end of class."
	status: "See notice at end of class."
	date: "$Date$"
	revision: "$Revision$"

class
	EB_LINEAR_METRIC_DEFINITION_AREA

inherit
	EB_LINEAR_METRIC_DEFINITION_AREA_IMP

	EB_METRIC_EDITOR
		undefine
			is_equal,
			copy,
			default_create
		redefine
			metric,
			expression_generator,
			is_linear_metric_editor
		end

	EB_CONSTANTS
		undefine
			is_equal,
			copy,
			default_create
		end

	EB_METRIC_INTERFACE_PROVIDER
		undefine
			is_equal,
			copy,
			default_create
		end

create
	make

feature {NONE} -- Initialization

	make (a_tool: like metric_tool) is
			-- Initialize `metric' with `a_metric' mode with `a_mode' and `unit' with `a_unit'.
		require
			a_tool_attached: a_tool /= Void
		do
			create expression_generator.make
			create change_actions
			default_create
			set_metric_tool (a_tool)
			setup_editor
		ensure
			metric_tool_set: metric_tool = a_tool
		end

	user_initialization is
			-- Called by `initialize'.
			-- Any custom user initialization that
			-- could not be performed in `initialize',
			-- (due to regeneration of implementation class)
			-- can be added here.
		local
			l_text: EV_TEXT
		do
				-- Initialize `metric_grid'.
			create metric_grid
			metric_grid.set_column_count_to (2)
			metric_grid.enable_row_separators
			metric_grid.enable_column_separators
			metric_grid.enable_single_row_selection
			metric_grid.column (1).set_title (metric_names.t_coefficient)
			metric_grid.column (2).set_title (metric_names.t_metrics)
			grid_area.extend (metric_grid)

			up_btn.remove_text
			up_btn.set_pixmap (pixmaps.icon_pixmaps.general_move_up_icon)
			down_btn.remove_text
			down_btn.set_pixmap (pixmaps.icon_pixmaps.general_move_down_icon)
			remove_metric_btn.remove_text
			remove_metric_btn.set_pixmap (pixmaps.icon_pixmaps.general_remove_icon)
			remove_all_metric_btn.remove_text
			remove_all_metric_btn.set_pixmap (pixmaps.icon_pixmaps.general_reset_icon)
				-- Setup actions.
			up_btn.select_actions.extend (agent on_up)
			down_btn.select_actions.extend (agent on_down)
			remove_all_metric_btn.select_actions.extend (agent on_remove_all_metrics)
			remove_metric_btn.select_actions.extend (agent on_remove_metric)
			metric_grid.set_item_veto_pebble_function (agent item_veto_pebble_function)
			metric_grid.item_drop_actions.extend (agent on_item_drop)
			create l_text
			expression_text.set_background_color (l_text.background_color)

			metric_grid.add_key_action (agent on_up, move_up_key_index)
			metric_grid.add_key_action (agent on_down, move_down_key_index)
			metric_grid.add_key_action (agent on_remove_metric, del_key_index)
			create del_key_shortcut.make_with_key_combination (create {EV_KEY}.make_with_code ({EV_KEY_CONSTANTS}.key_delete), False, False, False)
			create move_up_shortcut.make_with_key_combination (create {EV_KEY}.make_with_code ({EV_KEY_CONSTANTS}.key_numpad_subtract), True, False, False)
			create move_down_shortcut.make_with_key_combination (create {EV_KEY}.make_with_code ({EV_KEY_CONSTANTS}.key_numpad_add), True, False, False)
			metric_grid.add_key_shortcut (del_key_index, del_key_shortcut)
			metric_grid.add_key_shortcut (move_up_key_index, move_up_shortcut)
			metric_grid.add_key_shortcut (move_down_key_index, move_down_shortcut)
			up_btn.set_tooltip (metric_names.f_move_row_up + " (" + move_up_shortcut.out + ")")
			down_btn.set_tooltip (metric_names.f_move_row_down + " (" + move_down_shortcut.out + ")")
			remove_metric_btn.set_tooltip (metric_names.f_del_row + " (" + del_key_shortcut.out + ")")
			remove_all_metric_btn.set_tooltip (metric_names.f_clear_rows)
		ensure then
			del_key_shortcut_attached: del_key_shortcut /= Void
			ctrl_up_shortcut_attached: move_up_shortcut /= Void
			ctrl_down_shortcut_attached: move_down_shortcut /= Void
			metric_grid_attached: metric_grid /= Void
		end

feature -- Status report

	is_linear_metric_editor: BOOLEAN is True
			-- Is current a linear metric editor?

feature -- Setting

	set_mode (a_mode: INTEGER) is
			-- Set `mode' with `a_mode'.
		do
			mode := a_mode
		end

	load_metric_definition (a_metric: like metric) is
			-- Load `a_metric' in current editor.
		do
			load_metric_name_and_description (a_metric, mode = readonly_mode)
			if a_metric = Void then
					-- For new metric				
				load_variable_metric (create {EB_METRIC_LINEAR}.make ("Unnamed linear metric", unit))
			else
				load_variable_metric (a_metric)
			end
			on_change
		end

	enable_edit is
			-- Enable edit in current editor.
		do
			metric_grid.enable_sensitive
			toolbar_area.enable_sensitive
		end

	disable_edit is
			-- Disable edit in current editor.
		do
			metric_grid.disable_sensitive
			toolbar_area.disable_sensitive
		end

feature -- Access

	data_in_row (a_row: EV_GRID_ROW): TUPLE [criterion_name: STRING; coefficient: STRING] is
			-- Data in `a_row'
		require
			a_row_attached: a_row /= Void
			a_not_parented: a_row.parent = metric_grid
		local
			l_metric_item: EV_GRID_LABEL_ITEM
			l_coefficient_item: EV_GRID_EDITABLE_ITEM
		do
			if a_row.data /= Void then
				l_coefficient_item ?= a_row.item (1)
				l_metric_item ?= a_row.item (2)
				check
					l_coefficient_item /= Void
					l_metric_item /= Void
				end
				Result := [l_metric_item.text.out, l_coefficient_item.text.out]
			end
		end

	metric: EB_METRIC_LINEAR is
			-- Metric in current editor			
		local
			l_criterion_list: ARRAYED_LIST [STRING]
			l_coefficient_list: ARRAYED_LIST [DOUBLE]
			l_index: INTEGER
			l_count: INTEGER
			l_data: TUPLE [metric: STRING; coefficient: STRING]
		do
			create Result.make (name_area.name, unit)
			Result.set_description (name_area.description)
			create l_criterion_list.make (metric_grid.row_count - 1)
			create l_coefficient_list.make (metric_grid.row_count - 1)
			from
				l_index := 1
				l_count := metric_grid.row_count - 1
			until
				l_index > l_count
			loop
				l_data := data_in_row (metric_grid.row (l_index))
				if l_data /= Void then
					Result.variable_metric.extend (l_data.metric)
					if l_data.coefficient.is_double then
						Result.coefficient.extend (l_data.coefficient.to_double)
					else
						Result.coefficient.extend (0)
					end
				end
				l_index := l_index + 1
			end
		end

	metric_type: INTEGER is
			-- Type of metric in current editor
		do
			Result := linear_metric_type
		end

	definition_area_widget: EV_WIDGET is
			-- Definition area
		do
			Result := Current
		end

	metric_grid: ES_GRID
			-- Grid to display current metric

	expression_generator: EB_METRIC_LINEAR_EXPRESSION_GENERATOR
			-- Expression generator

feature{NONE} -- Implementation/Actions

	load_metric_in_row (a_name: STRING; a_coefficient: STRING; a_row: EV_GRID_ROW) is
			-- Load metric named `a_name' with `a_coefficient' in `a_row'.
		require
			a_name_attached: a_name /= Void
			a_row_attached: a_row /= Void
			a_row_parented: a_row.parent /= Void
		local
			l_coefficient_item: EV_GRID_EDITABLE_ITEM
			l_metric_item: EV_GRID_LABEL_ITEM
			l_metric: EB_METRIC
		do
			create l_coefficient_item.make_with_text (a_coefficient.out)
			create l_metric_item.make_with_text (a_name)
			l_metric_item.set_foreground_color (color_of_metric (a_name))
			if metric_manager.has_metric (a_name) then
				l_metric := metric_manager.metric_with_name (a_name)
				l_metric_item.set_pixmap (pixmap_from_metric (l_metric))
			else
				l_metric_item.set_pixmap (pixmaps.icon_pixmaps.general_error_icon)
			end
			l_coefficient_item.pointer_double_press_actions.force_extend (agent l_coefficient_item.activate)
			l_coefficient_item.deactivate_actions.extend (agent on_change)
			l_coefficient_item.set_text_validation_agent (agent (a_text: STRING_32): BOOLEAN do Result := a_text.is_real end)
			a_row.set_item (1, l_coefficient_item)
			a_row.set_item (2, l_metric_item)
			a_row.set_data (a_name)
		end

	item_veto_pebble_function (a_item: EV_GRID_ITEM; a_pebble: ANY): BOOLEAN is
			-- Function to veto `a_pebble' on `a_item'.
		local
			l_metric: EB_METRIC
		do
			l_metric ?= a_pebble
			if l_metric /= Void and then l_metric.unit = unit and then a_item /= Void and then a_item.is_parented then
				Result := True
			end
		end

	on_item_drop (a_item: EV_GRID_ITEM; a_pebble: ANY) is
			-- Action to be performed when `a_pebble' is dropped on `a_item'
		require
			a_item_attached: a_item /= Void
		local
			l_metric: EB_METRIC
			l_row: EV_GRID_ROW
			l_row_index: INTEGER
		do
			l_metric ?= a_pebble
			if l_metric /= Void and then a_item /= Void and then a_item.is_parented then
				l_row := a_item.row
				l_row_index := l_row.index
				metric_grid.insert_new_row (l_row_index)
				l_row := metric_grid.row (l_row_index)
				load_metric_in_row (l_metric.name, "1", l_row)
				metric_grid.remove_selection
				l_row.enable_select
				on_change
			end
		end

	on_remove_metric is
			-- Action to be performed when removing a selected row
		local
			l_row: EV_GRID_ROW
		do
			if not metric_grid.selected_rows.is_empty then
				l_row := metric_grid.selected_rows.first
				if l_row.data /= Void then
					metric_grid.remove_row (l_row.index)
					on_change
				end
			end
		end

	on_remove_all_metrics is
			-- Action to be performed when remove all rows
		do
			if metric_grid.row_count > 1 then
				metric_grid.remove_rows (1, metric_grid.row_count - 1)
				on_change
			end
		end

	on_up is
			-- Action to be performed when move selected row up
		local
			l_row: EV_GRID_ROW
			l_new_row: EV_GRID_ROW
			l_index: INTEGER
			l_data: TUPLE [metric: STRING; coefficient: STRING]
		do
			if not metric_grid.selected_rows.is_empty then
				l_row := metric_grid.selected_rows.first
				l_index := l_row.index
				if l_row.data /= Void and then l_index > 1 then
					metric_grid.insert_new_row (l_index - 1)
					l_new_row := metric_grid.row (l_index - 1)
					l_data := data_in_row (l_row)
					load_metric_in_row (l_data.metric, l_data.coefficient, l_new_row)
					metric_grid.remove_selection
					l_new_row.enable_select
					metric_grid.remove_row (l_row.index)
					on_change
				end
			end
		end

	on_down is
			-- Action to be performed when move selected row down
		local
			l_row: EV_GRID_ROW
			l_new_row: EV_GRID_ROW
			l_index: INTEGER
			l_data: TUPLE [metric: STRING; coefficient: STRING]
		do
			if not metric_grid.selected_rows.is_empty then
				l_row := metric_grid.selected_rows.first
				l_index := l_row.index
				if l_row.data /= Void and then l_index < metric_grid.row_count - 1 then
					metric_grid.insert_new_row (l_index + 2)
					l_new_row := metric_grid.row (l_index + 2)
					l_data := data_in_row (l_row)
					load_metric_in_row (l_data.metric, l_data.coefficient, l_new_row)
					metric_grid.remove_selection
					l_new_row.enable_select
					metric_grid.remove_row (l_row.index)
					on_change
				end
			end
		end

	on_change is
			-- Action to be performed when definition of current linear metric changes
		do
			expression_generator.set_metric (metric)
			expression_generator.generate_expression
			expression_generator.load_expression (expression_text)
			on_definition_change
		end

feature{NONE} -- Implementation

	load_variable_metric (a_metric: like metric) is
			-- Load variable metrics of `a_metric' in `metric_grid'.
		require
			a_metric_attached: a_metric /= Void
		local
			l_row: EV_GRID_ROW
			l_grid_lbl: EV_GRID_LABEL_ITEM
		do
			if metric_grid.row_count > 0 then
				metric_grid.remove_rows (1, metric_grid.row_count)
			end
			from
				a_metric.variable_metric.start
				a_metric.coefficient.start
			until
				a_metric.variable_metric.after or a_metric.coefficient.after
			loop
				metric_grid.insert_new_row (metric_grid.row_count + 1)
				l_row := metric_grid.row (metric_grid.row_count)
				load_metric_in_row (a_metric.variable_metric.item, a_metric.coefficient.item.out, l_row)
				a_metric.variable_metric.forth
				a_metric.coefficient.forth
			end

				-- Insert new line.
			metric_grid.insert_new_row (metric_grid.row_count + 1)
			l_row := metric_grid.row (metric_grid.row_count)
			create l_grid_lbl.make_with_text ("...")
			l_grid_lbl.set_tooltip (metric_names.f_drop_metric_here)
			l_row.set_item (1, l_grid_lbl)
			create l_grid_lbl.make_with_text ("...")
			l_grid_lbl.set_tooltip (metric_names.f_drop_metric_here)
			l_row.set_item (2, l_grid_lbl)
			resize_metric_grid
		end

	resize_metric_grid is
			-- Resize `metric_grid' properly.
		local
			l_width: INTEGER
			l_font: EV_FONT
		do
			if metric_grid.row_count > 0 then
				create l_font
				l_width := (l_font.string_width (metric_grid.column (1).title) + 20).max (metric_grid.column (1).required_width_of_item_span (1, metric_grid.row_count))
				metric_grid.column (1).set_width (l_width)
				metric_grid.column (2).resize_to_content
			end
		end

	color_of_metric (a_name: STRING): EV_COLOR is
			-- Color used to display metric named `a_name' in grid row
		require
			a_name_attached: a_name /= Void
		local
			l_manager: like metric_manager
		do
			l_manager := metric_manager
			if l_manager.has_metric (a_name) and then l_manager.is_metric_valid (a_name) then
				Result := (black_color)
			else
				Result := (red_color)
			end
		ensure
			result_attached: Result /= Void
		end

feature -- Key shortcuts

	del_key_index: INTEGER is 1
			-- Key index to delete a criterion

	move_up_key_index: INTEGER is 2
			-- Key index for move a criterion up

	move_down_key_index: INTEGER is 3
			-- Key index for move a criterion down

	del_key_shortcut: ES_KEY_SHORTCUT
			-- Del key

	move_up_shortcut: ES_KEY_SHORTCUT
			-- Ctrl + Up key combination

	move_down_shortcut: ES_KEY_SHORTCUT
			-- Ctrl + Down key combination

invariant
	metric_grid_attached: metric_grid /= Void

indexing
	copyright:	"Copyright (c) 1984-2006, Eiffel Software"
	license:	"GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options:	"http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful,	but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the	GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301  USA
		]"
	source: "[
			 Eiffel Software
			 356 Storke Road, Goleta, CA 93117 USA
			 Telephone 805-685-1006, Fax 805-685-6869
			 Website http://www.eiffel.com
			 Customer support http://support.eiffel.com
		]"

end -- class EB_LINEAR_METRIC_DEFINITION_AREA

