indexing
	legal: "See notice at end of class."
	status: "See notice at end of class."
-- System level server file counter.

class
	FILE_COUNTER

inherit
	COMPILER_COUNTER
		redefine
			append
		end

create
	make

feature -- Initialization

	append (other: like Current) is
			-- Append ids generated by `other' to `Current' and
			-- renumber the resulting set of ids.
		do
			Precursor {COMPILER_COUNTER} (other)
			if other.precompiled_offsets = Void then
				create precompiled_offsets.make (1,1)
				precompiled_offsets.put (precompiled_offset, 1)
			else
				increase_precompiled_offsets (other.precompiled_offsets)
			end
		end

feature -- Access

	compilation_id (id: INTEGER): INTEGER is
			-- Compilation_id corresponding to `id'.
		require
			precompiled_offsets_set: precompiled_offsets /= Void
		local
			i, nb: INTEGER
			found: BOOLEAN
		do
			from
				i := precompiled_offsets.lower
				nb := precompiled_offsets.upper
			until
				i > nb or found
			loop
				found := id <= precompiled_offsets.item (i)
				if found then
					Result := i
				end
				i := i + 1
			end
		ensure
			found: Result > 0
		end

feature -- Precompiled_offsets

	precompiled_offsets: ARRAY [INTEGER]
			-- Offset for each precompiled library.

feature {NONE} -- Implementation

	increase_precompiled_offsets (other: like precompiled_offsets) is
			-- Reset `precompiled_offsets' with new values.
		local
			i, nb: INTEGER
		do
			from
				create precompiled_offsets.make (1, other.count + 1)
				i := other.lower
				nb := other.upper
			until
				i > nb
			loop
				precompiled_offsets.put (other.item (i), i)
				i := i + 1
			end
			precompiled_offsets.put (precompiled_offset, nb + 1)
		end

indexing
	copyright:	"Copyright (c) 1984-2006, Eiffel Software"
	license:	"GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options:	"http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful,	but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the	GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301  USA
		]"
	source: "[
			 Eiffel Software
			 356 Storke Road, Goleta, CA 93117 USA
			 Telephone 805-685-1006, Fax 805-685-6869
			 Website http://www.eiffel.com
			 Customer support http://support.eiffel.com
		]"

end -- class FILE_COUNTER
