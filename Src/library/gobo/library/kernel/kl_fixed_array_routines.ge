indexing

	description:

		"Routines that ought to be in class FIXED_ARRAY. %
		%A fixed array is a zero-based indexed sequence of values, %
		%equipped with features `put', `item' and `count'."

	library:    "Gobo Eiffel Kernel Library"
	author:     "Eric Bezault <ericb@gobo.demon.co.uk>"
	copyright:  "Copyright (c) 1999, Eric Bezault"
	date:       "$Date$"
	revision:   "$Revision$"

class KL_FIXED_ARRAY_ROUTINES [G]

inherit

	KL_IMPORTED_FIXED_ARRAY_TYPE [G]

feature -- Initialization

	make (n: INTEGER): like FIXED_ARRAY_TYPE is
			-- Create a new fixed array being able to contain `n' items.
		require
			non_negative_n: n >= 0
#ifdef ISE
		local
			an_array: ARRAY [G]
		do
			!! an_array.make (0, n - 1)
			Result := an_array.area
#else
		do
			!! Result.make (0, n - 1)
#endif
		ensure
			fixed_array_not_void: Result /= Void
			valid_fixed_array: valid_fixed_array (Result)
			count_set: Result.count = n
		end

	make_from_array (an_array: ARRAY [G]): like FIXED_ARRAY_TYPE is
			-- Create a new fixed array with items from `an_array'.
		require
			an_array_not_void: an_array /= Void
		local
			array_routines: KL_ARRAY_ROUTINES [G]
#ifdef ISE
			a: ARRAY [G]
#endif
		do
			!! array_routines
#ifdef ISE
			a := array_routines.make_from_array (an_array, 0)
			Result := a.area
#else
			Result := array_routines.make_from_array (an_array, 0)
#endif
		ensure
			fixed_array_not_void: Result /= Void
			valid_fixed_array: valid_fixed_array (Result)
			count_set: Result.count = an_array.count
--			same_items: forall i in 0.. (Result.count - 1),
--				Result.item (i) = an_array.item (an_array.lower + i)
		end

feature -- Conversion

	to_fixed_array (an_array: ARRAY [G]): like FIXED_ARRAY_TYPE is
			-- Fixed array filled with items from `an_array'.
			-- The fixed array and `an_array' may share internal
			-- data. Use `make_from_array' if this is a concern.
		require
			an_array_not_void: an_array /= Void
		do
#ifdef ISE
			Result := an_array.area
#else
			if an_array.lower = 0 then
				Result := an_array
			else
				Result := make_from_array (an_array)
			end
#endif
		ensure
			fixed_array_not_void: Result /= Void
			valid_fixed_array: valid_fixed_array (Result)
			count_set: Result.count >= an_array.count
--			same_items: forall i in 0.. (an_array.count - 1),
--				Result.item (i) = an_array.item (an_array.lower + i)
		end

feature -- Status report

	valid_fixed_array (an_array: like FIXED_ARRAY_TYPE): BOOLEAN is
			-- Make sure that the lower bound of `an_array' is zero.
		require
			an_array_not_void: an_array /= Void
		do
#ifdef ISE
			Result := True
#else
			Result := an_array.lower = 0
#endif
		end

feature -- Resizing

	resize (an_array: like FIXED_ARRAY_TYPE; n: INTEGER): like FIXED_ARRAY_TYPE is
			-- Resize `an_array' so that it contains `n' items.
			-- Do not lose any previously entered items.
			-- Note: the returned fixed array  might be `an_array'
			-- or a newly created fixed array where items from
			-- `an_array' have been copied to.
		require
			an_array_not_void: an_array /= Void
			valid_fixed_array: valid_fixed_array (an_array)
			n_large_enough: n >= an_array.count
		do
#ifdef ISE
			Result := c_arycpy ($an_array, n, 0, an_array.count)
#else
			Result := an_array
			Result.resize (0, n - 1)
#endif
		ensure
			fixed_array_not_void: Result /= Void
			valid_fixed_array: valid_fixed_array (Result)
			count_set: Result.count = n
		end

feature -- Removal

	clear_all (an_array: like FIXED_ARRAY_TYPE) is
			-- Reset all items to default values.
		require
			an_array_not_void: an_array /= Void
			valid_fixed_array: valid_fixed_array (an_array)
#ifndef ISE || HACT || TOWER
		local
			i, nb: INTEGER
			v: G
		do
			nb := an_array.upper
			from until i > nb loop
				an_array.put (v, i)
				i := i + 1
			end
#else
		do
#ifdef ISE
			c_spclearall ($an_array)
#else
			an_array.clear_all
#endif
#endif
		ensure
			-- all_cleared: forall i in 0..(an_array.count - 1),
			--		an_array.item (i) = Void or else
			--		an_array.item (i) = an_array.item (i).default
		end

#ifdef ISE
feature {NONE} -- Externals

	c_arycpy (old_area: POINTER; newsize, s, n: INTEGER): SPECIAL [G] is
			-- New area of size `newsize' containing `n' items
			-- from `oldarea'.
			-- Old items are at position `s' in new area.
		external
			"C | %"eif_misc.h%""
		alias
			"arycpy"
		end

	c_spclearall (p: POINTER) is
			-- Reset all items to default value.
		external
			"C | %"eif_copy.h%""
		alias
			"spclearall"
		end

#endif
end -- class KL_FIXED_ARRAY_ROUTINES
