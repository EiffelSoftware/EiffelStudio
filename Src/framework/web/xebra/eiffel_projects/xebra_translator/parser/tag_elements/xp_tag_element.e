note
	description: "[
		A {XP_TAG_ELEMENT} is generated by parsing a "xeb"-file. {XP_TAG_ELEMENT}s
		are combined to trees.
	]"
	date: "$Date$"
	revision: "$Revision$"

class
	XP_TAG_ELEMENT

create
	make

feature {NONE}-- Initialization

	make (a_namespace: STRING; a_id: STRING; a_class_name: STRING; a_debug_information: STRING)
			-- `a_class_name': The name of the corresponding TAG-class
		require
			a_id_valid: not a_id.is_empty
			a_class_name_is_valid: not a_class_name.is_empty
			a_namespace_is_valid: a_namespace /= Void
		do
			namespace := a_namespace
			class_name := a_class_name
			id := a_id
			create {ARRAYED_LIST [XP_TAG_ELEMENT]} children.make (3)
			create {HASH_TABLE [STRING, STRING]} parameters.make (3)
			debug_information := a_debug_information
		end

feature {NONE} -- Access

	class_name: STRING
			-- The name of the corresponding TAG-class

	parameters: HASH_TABLE [STRING, STRING]
			-- The parameters of the tag [value, parameter id]

	debug_information: STRING
			-- Debug information (row and column in the xeb file)

feature -- Access

	controller_id: STRING assign set_controller_id
			-- The id of the controller which should be used

	set_controller_id (a_id: STRING)
		do
			controller_id := a_id
		end

	retrieve_value (a_id: STRING): STRING
			-- Retrieves the value of the parameter with the the id `a_id'
		do
			Result := parameters [a_id]
		end

	children: LIST [XP_TAG_ELEMENT]
			-- All the children of the tag

	id: STRING
			-- The tag id

	namespace: STRING
			-- The namespace (tag library) of the tag

	multiline_argument: BOOLEAN assign set_multiline_argument
			-- Are the arguments of the tag multiline and have to be escaped?

	set_multiline_argument (is_multiline: BOOLEAN)
			-- Are the arguments of the tag multiline and have to be escaped?
		do
			multiline_argument := is_multiline
		end

	has_children: BOOLEAN
			-- Are there any children?
		do
			Result := not children.is_empty
		end

	has_attribute (name: STRING): BOOLEAN
			-- Does the tag already have an attribute with the id `name'
		do
			Result := parameters.has_key (name)
		end

feature --Basic Implementation

	put_subtag (child: like Current)
			-- Adds a tag to the list of children.
		do
			children.extend (child)
		ensure
			child_has_been_added: old children.count + 1 = children.count
		end

	put_attribute (a_local_part: STRING; a_value: STRING)
			-- Sets the attribute of this tag.
		require
			local_part_is_not_empty: not a_local_part.is_empty
			value_is_not_empty: not a_value.is_empty
		do
			parameters.extend (a_value, a_local_part)
		ensure
			attribute_has_been_added: old parameters.count + 1 = parameters.count
		end

	build_tag_tree (
				a_feature: XEL_FEATURE_ELEMENT;
				templates: LIST [XGEN_SERVLET_GENERATOR_GENERATOR];
				root_template: XGEN_SERVLET_GENERATOR_GENERATOR;
				leftover_regions: HASH_TABLE [LIST [XP_TAG_ELEMENT], STRING])
			-- Adds the needed expressions which build the tree of Current with the correct classes
		do
			internal_build_tag_tree (a_feature, templates, root_template, True, leftover_regions)
		end

	copy_tag_tree: like Current
			-- Copies the tag and its children
		do
			create Result.make (namespace, id, class_name, debug_information)
			Result.multiline_argument := multiline_argument
			Result.controller_id := controller_id
			from
				parameters.start
			until
				parameters.after
			loop
				Result.put_attribute (parameters.key_for_iteration, parameters.item_for_iteration)
				parameters.forth
			end
			from
				children.start
			until
				children.after
			loop
				Result.put_subtag (children.item.copy_tag_tree)
				children.forth
			end
		end

	accept (visitor: XP_TAG_ELEMENT_VISITOR)
			-- Element part of the Visitor Pattern
		do
			visitor.visit_tag_element (Current)
			from
				children.start
			until
				children.after
			loop
				children.item.accept (visitor)
				children.forth
			end
		end

feature {XP_TAG_ELEMENT} -- Implementation

	internal_build_tag_tree (
					a_feature: XEL_FEATURE_ELEMENT;
					templates: LIST [XGEN_SERVLET_GENERATOR_GENERATOR];
					root_template: XGEN_SERVLET_GENERATOR_GENERATOR;
					is_root: BOOLEAN;
					leftover_regions: HASH_TABLE [LIST [XP_TAG_ELEMENT], STRING])
				-- Adds the needed expressions which build the tree of Current with the correct classes
		require
			controller_id_set: attached controller_id -- Doesn't work?
		do
			a_feature.append_comment (debug_information)
			a_feature.append_expression ("create {" + class_name + "} temp.make")
			a_feature.append_expression ("temp.debug_information := %"" + debug_information + "%"" )
			if is_root then
				a_feature.append_expression ("root_tag := temp")
			else
				a_feature.append_expression ("stack.item.add_to_body (temp)")
			end
			build_attributes (a_feature, parameters)
			if attached controller_id then
				a_feature.append_expression ("temp.current_controller_id := %"" + controller_id + "%"")
			end

			--build_attributes (a_feature, dynamic_parameters)

			if has_children then
				a_feature.append_expression ("stack.put (temp)")
				from
					children.start
				until
					children.after
				loop
					children.item.internal_build_tag_tree (a_feature, templates, root_template, False, leftover_regions)
					children.forth
				end
				a_feature.append_expression ("stack.remove")
			end
		end

	build_attributes (a_feature: XEL_FEATURE_ELEMENT; attributes: HASH_TABLE [STRING, STRING])
			-- Adds expressions which put the right attributes to the tags
		do
			from
				attributes.start
			until
				attributes.after
			loop
				if multiline_argument then
					a_feature.append_expression ("temp.put_attribute(%""
						+ attributes.key_for_iteration + "%", "
						+ "%N%"[%N" + attributes.item_for_iteration + "%N]%")"
					)
				else
					a_feature.append_expression ("temp.put_attribute(%""
						+ attributes.key_for_iteration + "%", "
						+ "%"" + attributes.item_for_iteration + "%")"
					)
				end
				attributes.forth
			end
		end

note
	copyright: "Copyright (c) 1984-2009, Eiffel Software"
	license: "GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options: "http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful, but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
		]"
	source: "[
			Eiffel Software
			5949 Hollister Ave., Goleta, CA 93117 USA
			Telephone 805-685-1006, Fax 805-685-6869
			Website http://www.eiffel.com
			Customer support http://support.eiffel.com
		]"
end
