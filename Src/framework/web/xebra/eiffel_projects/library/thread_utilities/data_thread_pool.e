note
	description: "Summary description for {DATA_THREAD_POOL_MANAGER}."
	legal: "See notice at end of class."
	status: "Community Preview 1.0"
	date: "$Date$"
	revision: "$Revision$"

class
	DATA_THREAD_POOL [G]

inherit
	THREAD_POOL [G]
		rename
			make as thread_pool_make
		redefine
			valid_action,
			initialize_threads
		end

create
	make

feature {NONE} -- Initialization

	make (n: NATURAL; a_target_spawner: FUNCTION [G])
			-- The targets are generated by the `a_target_spawner'.
			-- The maximal number of threads that can be spawned is stored in `n'.
			-- If this limit is reached, the requests are queued, until one of the threads is completed.
		require
			data_spawner_has_no_open_arguments: a_target_spawner.valid_operands (Void)
			n_positive: n > 0
			n_not_too_large: n < {INTEGER_32}.max_value.as_natural_32
		do
			target_spawner := a_target_spawner
			thread_pool_make (n)
		end

feature -- Status report

	valid_action (a_action: PROCEDURE): BOOLEAN
			-- <Precursor>
		do
				-- We need at most one open operands of type G.
			Result := attached {PROCEDURE [G]} a_action and then
				a_action.empty_operands.count = 1
		end

feature {NONE} -- Implementation: Access

	target_spawner: FUNCTION [G]
			-- Spawns new targets for work

	initialize_threads
			-- <Precursor>
			-- Added target set for reuse of target
		local
			i: NATURAL
			thread: POOLED_THREAD [G]
		do
			from
				i := 1
			until
				i > capacity
			loop
				create thread.make (Current, work_semaphore)
				thread.set_target (target_spawner.item (Void))
				thread.launch
				i := i + 1
			end
		end

note
	copyright: "Copyright (c) 1984-2009, Eiffel Software"
	license: "GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options: "http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful, but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
		]"
	source: "[
			Eiffel Software
			5949 Hollister Ave., Goleta, CA 93117 USA
			Telephone 805-685-1006, Fax 805-685-6869
			Website http://www.eiffel.com
			Customer support http://support.eiffel.com
		]"
end
