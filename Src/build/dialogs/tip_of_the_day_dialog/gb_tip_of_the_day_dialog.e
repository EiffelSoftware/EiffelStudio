indexing
	description: "Objects that represent an EV_DIALOG.%
		%The original version of this class was generated by EiffelBuild."
	legal: "See notice at end of class."
	status: "See notice at end of class."
	author: ""
	date: "$Date$"
	revision: "$Revision$"

class
	GB_TIP_OF_THE_DAY_DIALOG

inherit
	GB_TIP_OF_THE_DAY_DIALOG_IMP
		export
			{NONE} show, show_modal_to_window, show_relative_to_window
		end

	GB_SHARED_PREFERENCES
		undefine
			default_create, copy
		end

	GB_TIPS
		export
			{NONE} all
		undefine
			default_create, copy
		end

	GB_SHARED_PIXMAPS
		export
			{NONE} all
		undefine
			default_create, copy
		end

create
	make_with_components

feature {NONE} -- Initialization

	components: GB_INTERNAL_COMPONENTS
		-- Access to a set of internal components for an EiffelBuild instance.

	make_with_components (a_components: GB_INTERNAL_COMPONENTS) is
			-- Create `Current' and assign `a_components' to `components'.
		require
			a_components_not_void: a_components /= Void
		do
			components := a_components
			default_create
		ensure
			components_set: components = a_components
		end

feature {NONE} -- Initialization

	user_initialization is
			-- called by `initialize'.
			-- Any custom user initialization that
			-- could not be performed in `initialize',
			-- (due to regeneration of implementation class)
			-- can be added here.
		do
			set_default_cancel_button (close_button)
			set_default_push_button (close_button)
			tip_counter := preferences.global_data.tip_of_day_index
			if preferences.global_data.show_tip_of_the_day then
				show_tips_button.enable_select
			else
				show_tips_button.disable_select
			end
			tip_label.resize_actions.force_extend (agent show_tip)
			tip_label.set_minimum_width (100)
			show_actions.force_extend (agent show_tip)
			set_icon_pixmap (Icon_build_window @ 1)
		end

feature -- Basic operations

	show_modal_and_centered_to_window (a_window: EV_WINDOW) is
			-- Show `Current' modal to an centered on `a_window'.
		require
			a_window_not_void: a_window /= Void
		do
			tip_counter := preferences.global_data.tip_of_day_index
			set_position (a_window.x_position + a_window.width // 2 - width // 2,
					a_window.y_position + a_window.height // 2 - height // 2)
			show_modal_to_window (a_window)
		end

feature {NONE} -- Implementation

	tip_counter: INTEGER
		-- Current tip to be displayed.

	next_tip_selected is
			-- Called by `select_actions' of `next_tip_button'.
		do
			tip_counter := get_next_tip_index (tip_counter)
			set_text (all_tips @ tip_counter)
		end

	get_next_tip_index (current_index: INTEGER): INTEGER is
			-- `Result' is next valid tip index based on `current_index'.
		do
			Result := current_index + 1
			if result > tip_count then
				Result := 1
			end
		ensure
			Result_valid: Result >= 1 and Result <= tip_count
		end

	close_button_selected is
			-- Called by `select_actions' of `close_button'.
		do
			preferences.global_data.show_tip_of_the_day_preference.set_value (show_tips_button.is_selected)
			preferences.global_data.tip_of_day_index_preference.set_value (get_next_tip_index (tip_counter))
			preferences.preferences.save_resources
			hide
		end

feature {NONE} -- Implementation

	previous_lines: ARRAYED_LIST [STRING] is
			-- Previous contents of lines displayed.
			-- Used to prevent continuous redrawing of label when
			-- nothign has changed.
		once
			create Result.make (20)
		end

	show_tip is
			-- Display current tip on `tip_label'.
		do
			set_text (all_tips @ tip_counter)
		end

	set_text (tip: STRING) is
			-- Display `tip' as a wrapped text within `tip_label'.
			-- Replace all '%N' characters as spaces.
		local
			counter: INTEGER
			font: EV_FONT
			current_width: INTEGER
			last_string: STRING
			temp_string: STRING
			modified_tip: STRING
			lines: ARRAYED_LIST [STRING]
			start_pos: INTEGER
			output: STRING
			maximum_string_width: INTEGER
			lines_changed: BOOLEAN
			all_space_indexes: ARRAYED_LIST [INTEGER]
		do
			create all_space_indexes.make (20)
			create lines.make (4)
			font := tip_label.font
			modified_tip := tip.twin
			modified_tip.replace_substring_all ("%N", " ")
			modified_tip.append_character (' ')
			maximum_string_width := tip_label.width - 10

				-- Set up all space indexes which stores the index of each space in the
				-- text, as these are the wrapping criterion.
				-- Note that if a word is contained that is longer than the width of the label,
				-- this will probable lead to problems. No attempt to prevent this is made in the code.
			from
				counter := 1
			until
				counter > modified_tip.count
			loop
				if modified_tip.item (counter).is_equal(' ') then
					all_space_indexes.extend (counter)
				end
				counter := counter + 1
			end

				-- Perform calculations to determine where wrapping must occur.
			from
				start_pos := 1
				counter := 1
			until
				counter > all_space_indexes.count
			loop
				from
					current_width := 0
				until
					current_width > maximum_string_width or
					counter > all_space_indexes.count
				loop

					temp_string := modified_tip.substring (start_pos, all_space_indexes.i_th (counter) - 1)
					current_width := font.string_width (temp_string)
					if current_width <= maximum_string_width then
						last_string := temp_string
						counter := counter + 1
					else
						counter := counter - 1
					end
				end
				if all_space_indexes.valid_index (counter) then
					start_pos := all_space_indexes.i_th (counter) + 1
				end
				lines.extend (last_string)
			end

				-- Now determine if the contents of the line have actually changed.
				-- If they have not, then there is no need to set the text again, as it
				-- causes flicker.
			from
				lines.start
				Previous_lines.start
			until
				lines.off or lines_changed or previous_lines.off
			loop
				if lines.item.count /= previous_lines.item.count then
					lines_changed := True
				end
				lines.forth
				Previous_lines.forth
			end
			if previous_lines.is_empty then
				lines_changed := True
			end

				-- Now create and set the text on the label if
				-- it needs to be changed.
			if lines_changed then
				output := ""
				from
					lines.start
				until
					lines.off
				loop
					output.append (lines.item)
					if lines.index < lines.count then
						output.append_character ('%N')
					end
					lines.forth
				end
				previous_lines.make_from_array (lines)
				tip_label.set_text (output)
			end
		end

indexing
	copyright:	"Copyright (c) 1984-2006, Eiffel Software"
	license:	"GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options:	"http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful,	but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the	GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301  USA
		]"
	source: "[
			 Eiffel Software
			 356 Storke Road, Goleta, CA 93117 USA
			 Telephone 805-685-1006, Fax 805-685-6869
			 Website http://www.eiffel.com
			 Customer support http://support.eiffel.com
		]"


end -- class GB_TIP_OF_THE_DAY_DIALOG

