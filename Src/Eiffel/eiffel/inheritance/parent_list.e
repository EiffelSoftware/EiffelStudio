note
	legal: "See notice at end of class."
	status: "See notice at end of class."
-- Eiffel class generated by the 2.3 to 3 translator.

class PARENT_LIST

inherit
	FIXED_LIST [PARENT_C]

create
	make, make_filled

feature -- Validity

	check_validity2
			-- Check the redefine and select clause
		local
			i, nb: INTEGER
		do
			from
				nb := count
			until
				i = nb
			loop
				area [i].check_validity2
				i := i + 1
			end
		end

	check_validity4
			-- Check useless selection
		local
			l_area: SPECIAL [PARENT_C]
			i, nb: INTEGER
			p: PARENT_C
		do
			from
				l_area := area
				nb := count
			until
				i = nb
			loop
				p := l_area.item(i)
				if not (p.selecting = Void) then
					p.check_validity4
				end
				i := i + 1
			end
		end

	are_features_renamed: BOOLEAN
			-- Are features explicitly renamed in `Current' list?
		local
			i, nb: INTEGER
		do
			from
				nb := count
			until
				i = nb
			loop
				Result := area [i].has_renaming
				if Result then
					i := nb
				else
					i := i + 1
				end
			end
		end

	are_features_undefined: BOOLEAN
			-- Are features explicitly undefined in `Current' list?
		local
			i, nb: INTEGER
		do
			from
				nb := count
			until
				i = nb
			loop
				Result := area [i].has_undefining
				if Result then
					i := nb
				else
					i := i + 1
				end
			end
		end

	are_features_redefined: BOOLEAN
			-- Are features explicitly redefined in `Current' list?
		local
			i, nb: INTEGER
		do
			from
				nb := count
			until
				i = nb
			loop
				Result := area [i].has_redefining
				if Result then
					i := nb
				else
					i := i + 1
				end
			end
		end

	is_explicitly_selecting (a_feature_name_id: INTEGER): BOOLEAN
			-- Are the parents explicitly selecting `a_feature_name_id'?
		require
			good_argument: a_feature_name_id > 0
		local
			i, nb: INTEGER
		do
			from
				nb := count
			until
				Result or else i >= nb
			loop
				if not area [i].is_non_conforming then
						-- If there is a non-conforming branch then return True
						-- as the feature is implicitly selected.
					Result := area [i].is_selecting (a_feature_name_id)
				end
				i := i + 1
			end
		end

note
	copyright:	"Copyright (c) 1984-2006, Eiffel Software"
	license:	"GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options:	"http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful,	but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the	GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301  USA
		]"
	source: "[
			 Eiffel Software
			 356 Storke Road, Goleta, CA 93117 USA
			 Telephone 805-685-1006, Fax 805-685-6869
			 Website http://www.eiffel.com
			 Customer support http://support.eiffel.com
		]"

end -- class PARENT_LIST
