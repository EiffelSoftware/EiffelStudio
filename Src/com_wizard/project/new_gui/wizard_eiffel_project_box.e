indexing
	description: "Objects that represent an EV_TITLED_WINDOW.%
		%The original version of this class was generated by EiffelBuild."
	date: "$Date$"
	revision: "$Revision$"

class
	WIZARD_EIFFEL_PROJECT_BOX

inherit
	WIZARD_EIFFEL_PROJECT_BOX_IMP

	WIZARD_VALIDITY_CHECKER
		undefine
			default_create,
			is_equal,
			copy
		end

feature {NONE} -- Initialization

	user_initialization is
			-- called by `initialize'.
		do
			initialize_checker
			epr_box.setup ("Path to Eiffel project file (*.epr):", "epr_key", agent valid_file_path (?, "Invalid path to Eiffel project file"), create {ARRAYED_LIST [TUPLE [STRING, STRING]]}.make_from_array (<<["*.epr", "Eiffel Project File (*.epr)"]>>), "Browse for Eiffel Project File")
			ace_file_box.setup ("Path to system's ace file (*.ace):", "ace_key", agent valid_file_path (?, "Invalid path to system's ace file"), create {ARRAYED_LIST [TUPLE [STRING, STRING]]}.make_from_array (<<["*.ace", "LACE File (*.ace)"], ["*.*", "All Files (*.*)"]>>), "Browse for system's ace file")
			facade_box.setup ("Name of Eiffel facade class:", "facade_key", agent is_valid_eiffel_identifier (?, "Invalid Eiffel facade class name"))
			facade_cluster_box.setup ("Name of Eiffel facade class cluster:", "cluster_key", agent is_valid_eiffel_identifier (?, "Invalid Eiffel facade class cluster"))
		end

feature -- Basic Operations

	save_values is
			-- Persist combo box entries for next session.
		do
			facade_box.save_combo_text
			facade_cluster_box.save_combo_text
		end
		
feature {NONE} -- Implementation

	valid_file_path (a_path, a_error_message: STRING): BOOLEAN is
			-- Is `a_path' a valid file path?
		require
			non_void_path: a_path /= Void
			non_void_error_message: a_error_message /= Void
		do
			Result := is_valid_file (a_path)
			set_error (Result, a_error_message)
		end
		
	is_valid_eiffel_identifier (a_string, a_error_message: STRING): BOOLEAN is
			-- Is `a_string' a valid eiffel identifier?
		local
			i, l_count: INTEGER;
			l_char: CHARACTER
		do
			l_count := a_string.count
			if l_count /= 0 and then a_string.item(1).is_alpha then
				from
					Result := True
					i := 2
				until
					i > l_count or else not Result
				loop
					l_char := a_string.item (i)
					Result := l_char.is_alpha or else l_char = '_' or else l_char.is_digit
					i := i + 1
				end
			end
			set_error (Result, a_error_message)
		end

end -- class WIZARD_EIFFEL_PROJECT_BOX

