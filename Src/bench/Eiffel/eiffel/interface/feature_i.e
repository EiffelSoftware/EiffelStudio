indexing
	description: "Instance of an Eiffel feature: for every inherited feature there is%N%
		%an instance of FEATURE_I with its final name, the class name where it%N%
		%is written, the body id of its content and the routine table ids to%N%
		%which the feature is attached.%N%
		%Attribute `type' is the real type of the feature in the class where it%N%
		%is inherited (or written), that means there is no more anchored type."
	date: "$Date$"
	version: "$Revision$"

deferred class FEATURE_I 

inherit
	SHARED_WORKBENCH

	SHARED_SERVER
		export
			{ANY} all
		end

	SHARED_INSTANTIATOR

	SHARED_ERROR_HANDLER

	SHARED_TYPES

	SHARED_EVALUATOR

	SHARED_ARG_TYPES

	SHARED_TABLE

	SHARED_AST_CONTEXT

	SHARED_BYTE_CONTEXT
		rename
			context as byte_context
		end

	SHARED_CODE_FILES

	SHARED_PATTERN_TABLE

	SHARED_INST_CONTEXT

	SHARED_ID_TABLES
		export
			{ANY} Body_index_table, Original_body_index_table
		end

	SHARED_ARRAY_BYTE

	SHARED_EXEC_TABLE

	HASHABLE
		rename
			hash_code as feature_name_id
		end

	COMPILER_EXPORTER

	SHARED_NAMES_HEAP
			
feature -- Access

	feature_name: STRING is
			-- Final name of the feature
		require
			feature_name_id_set: feature_name_id > 0
		do
			Result := Names_heap.item (feature_name_id)
		ensure
			Result_not_void: Result /= Void
			Result_not_empty: not Result.is_empty
		end

	feature_name_id: INTEGER
			-- Id of `feature_name' in `Names_heap' table.

	original_name_id: INTEGER
			-- Original Id name where feature was first defined. Even if 
			-- descendants of `written_in' rename current feature, `original_name_id'
			-- is preserved.
			
	feature_id: INTEGER
			-- Feature id: first key in the feature call hash table
			-- of a class: tow features of different names have two
			-- different feature ids.

	written_in: INTEGER
			-- Class id where feature is written

	implemented_in: INTEGER
			-- Class id where feature is defined to be implemented in an interface.
			-- Useful only in IL generation.

	belongs_to_interface: BOOLEAN
			-- Does current feature needs to be declared in
			-- corresponding interface of class being analyzed.
	
	body_index: INTEGER
			-- Index of body id

	pattern_id: INTEGER
			-- Id of the feature pattern

	rout_id_set: ROUT_ID_SET
			-- Routine table to which the feature belongs to.

	export_status: EXPORT_I
			-- Export status of the feature

	is_origin: BOOLEAN
			-- Is the feature an origin ?

	is_frozen: BOOLEAN
			-- Is the feature frozen ?

	is_selected: BOOLEAN
			-- Is the feature selected ?

	is_infix: BOOLEAN
			-- Is the feature an infixed one ?

	is_prefix: BOOLEAN
			-- Is the feature a prefixed one ?

	external_name: STRING is
			-- External name
		require
			external_name_id_set: external_name_id > 0
		do
			Result := Names_heap.item (external_name_id)
		ensure
			Result_not_void: Result /= Void
			Result_not_empty: not Result.is_empty
		end;

	external_name_id: INTEGER is
			-- External name of feature if any generation.
		do
			Result := private_external_name_id
			if Result = 0 then
				Result := feature_name_id
			end
		end

feature -- Debugger access

	number_of_breakpoint_slots: INTEGER is
			-- Number of breakpoint slots in the feature (:::)
			-- It includes the pre/postcondition (inner & herited)
			-- and the rescue clause.
			--
			--|---------------------------------------------------------
			--| Note from Arnaud PICHERY [ aranud@mail.dotcom.fr ]     |
			--|---------------------------------------------------------
			--| If the feature inherits from one routine that has no   |
			--| precondition, the precondition for this routine will   |
			--| always be true and thus will not be generated by the   |
			--| compiler. So, we do not count the preconditions if one |
			--| of the inherited routines has an empty precondition    |
			--| clause.                                                |
			--|---------------------------------------------------------
		local
			loc_assert_id_set		: ASSERT_ID_SET
			inh_assert_info			: INH_ASSERT_INFO
			i						: INTEGER
		do
				-- Get the number of breakpoint slots in the body + rescue +
				-- inner preconditions and inner postconditions
			Result := body.number_of_breakpoint_slots

				-- Add the number of breakpoint slots for inherited
				-- pre & postconditions.
			loc_assert_id_set := assert_id_set
			if loc_assert_id_set /= Void then
				from
					i := 1
				until
					i > loc_assert_id_set.count
				loop
					inh_assert_info := loc_assert_id_set @ i
					if loc_assert_id_set.has_precondition then
						Result := Result + inh_assert_info.precondition_count
					end
					Result := Result + inh_assert_info.postcondition_count
					i := i + 1
				end

					-- If the inherited assertions does not define a precondition,
					-- the precondition will be true and thus no precondition
					-- clause will be generated at all, so we have to remove the
					-- inner preconditions that we have already counted
					--
					-- Note: the inner preconditions have been added by the
					-- operation "Result := body.number_of_breakpoint_slots".
				if not loc_assert_id_set.has_precondition then
					Result := Result - number_of_precondition_slots
				end
			end
		end

	number_of_precondition_slots: INTEGER is 
			-- Number of preconditions
			-- (inherited assertions are not taken into account)
		do
			Result := body.number_of_precondition_slots
		end

	number_of_postcondition_slots: INTEGER is 
			-- Number of postconditions
			-- (inherited assertions are not taken into account)
		do
			Result := body.number_of_postcondition_slots
		end

feature -- Status

	to_melt_in (a_class: CLASS_C): BOOLEAN is
			-- Has the current feature to be melted in class `a_class' ?
		require
			good_argument: a_class /= Void
		do
			Result := a_class.class_id = written_in
		end

	to_generate_in (a_class: CLASS_C): BOOLEAN is
			-- Has the current feature to be generated in class `a_class' ?
		require
			good_argument: a_class /= Void
		do
			Result := a_class.class_id = written_in
		end

	frozen to_implement_in (a_class: CLASS_C): BOOLEAN is
			-- Does Current feature need to be exposed in interface 
			-- used for IL generation?
		require
			a_class_not_void: a_class /= Void
		do
			Result := a_class.class_id = written_in
		end

feature -- Setting

	set_feature_id (i: INTEGER) is
			-- Assign `i' to `feature_id'
		do
			feature_id := i
		end

	set_body_index (i: INTEGER) is
			-- Assign `i' to `body_index'.
		do
			body_index := i
		end

	set_pattern_id (i: INTEGER) is
			-- Assign `i' to `pattern_id'.
		do
			pattern_id := i
		end

	set_feature_name, set_renamed_name (s: STRING) is
			-- Assign `s' to `feature_name'.
			-- `set_renamed_name' is needed for C external
			-- routines that can't be renamed.
		require
			s_not_void: s /= Void
			s_not_empty: not s.is_empty
		local
			l_names_heap: like Names_heap
		do
			l_names_heap := Names_heap
			l_names_heap.put (s)
			feature_name_id := l_names_heap.found_item
		ensure
			feature_name_set: equal (feature_name, s)
		end

	set_feature_name_id, set_renamed_name_id (id: INTEGER) is
			-- Assign `id' to `feature_name_id'.
		require
			valid_id: Names_heap.valid_index (id)
		do
			feature_name_id := id
		ensure
			feature_name_id_set: feature_name_id = id
		end

	set_original_name_id (id: INTEGER) is
			-- Assign `id' to `original_name_id'.
		require
			valid_id: Names_heap.valid_index (id)
		do
			original_name_id := id
		ensure
			original_name_id_set: original_name_id = id
		end

	set_written_in (a_class_id: like written_in) is
			-- Assign `a_class_id' to `written_in'.
		require
			a_class_id_not_void: a_class_id > 0
		do
			written_in := a_class_id
		ensure
			written_in_set: written_in = a_class_id
		end

	set_implemented_in (a_class_id: like implemented_in) is
			-- Assign `a_class_id' to `implemented_in'.
		require
			a_class_id_not_void: a_class_id >= 0
		do
			implemented_in := a_class_id
		ensure
			implemented_in_set: implemented_in = a_class_id
		end

	set_belongs_to_interface (v: like belongs_to_interface) is
			-- Set `v' to `belongs_to_interface'
		do
			belongs_to_interface := v
		ensure
			belongs_to_interface: belongs_to_interface = v
		end

	set_is_origin (b: BOOLEAN) is
			-- Assign `b' to `is_origin'.
		do
			is_origin := b
		end

	set_export_status (e: EXPORT_I) is
			-- Assign `e' to `export_status'.
		do
			export_status := e
		end

	set_is_frozen (b: BOOLEAN) is
			-- Assign `b' to `is_frozen'.
			--|Note: nothing to do with melted/frozen features
		do
			is_frozen := b
		end

	set_is_infix (b: BOOLEAN) is
			-- Assign `b' to `is_infix'. 
		do
			is_infix := b
		end

	set_is_prefix (b: BOOLEAN) is
			-- Assign `b' to `is_prefix'.
		do
			is_prefix := b
		end

	set_is_selected (b: BOOLEAN) is
			-- Assign `b' to `is_selected'.
		do
			is_selected := b
		end

	set_rout_id_set (set: like rout_id_set) is
			-- Assign `set' to `rout_id_set'.
		do
			rout_id_set := set
		end

	set_private_external_name (n: like private_external_name) is
			-- Assign `n' to `private_external_name'.
		require
			n_not_empty: n /= Void implies not n.is_empty
		local
			l_names_heap: like Names_heap
		do
			l_names_heap := Names_heap
			l_names_heap.put (n)
			private_external_name_id := l_names_heap.found_item
		ensure
			private_external_name_set: n /= Void implies equal (private_external_name, n)
		end

	set_private_external_name_id (n_id: like private_external_name_id) is
			-- Assign `n_id' to `private_external_name_id'.
		require
			valid_n: n_id > 0
		do
			private_external_name_id := n_id
		ensure
			private_external_name_set: private_external_name_id = n_id
		end

	generation_class_id: INTEGER is
			-- Id of the class where the feature has to be generated in
		do
			Result := written_in
		end

	duplicate: like Current is
			-- Clone
		do
			Result := clone (Current)
		end

	duplicate_arguments is
			-- Do a clone of the arguments (for replication)
		do
			-- Do nothing
		end

feature -- Incrementality

	equiv (other: FEATURE_I): BOOLEAN is
			-- Incrementality test on instance of FEATURE_I during
			-- second pass.
		require
			good_argument: other /= Void
		do
			Result := written_in = other.written_in
				and then is_selected = other.is_selected
				and then rout_id_set.same_as (other.rout_id_set)
				and then is_origin = other.is_origin
				and then is_frozen = other.is_frozen
				and then is_deferred = other.is_deferred
				and then is_external = other.is_external
				and then export_status.same_as (other.export_status)
				and then same_signature (other)
				and then has_precondition = other.has_precondition
				and then has_postcondition = other.has_postcondition
debug ("ACTIVITY")
	if not Result then
			io.error.putbool (written_in = other.written_in) io.error.new_line;
			io.error.putbool (is_selected = other.is_selected) io.error.new_line;
			io.error.putbool (rout_id_set.same_as (other.rout_id_set)) io.error.new_line;
			io.error.putbool (is_origin = other.is_origin) io.error.new_line;
			io.error.putbool (is_frozen = other.is_frozen) io.error.new_line;
			io.error.putbool (is_deferred = other.is_deferred) io.error.new_line;
			io.error.putbool (is_external = other.is_external) io.error.new_line;
			io.error.putbool (export_status.same_as (other.export_status)) io.error.new_line;
			io.error.putbool (same_signature (other)) io.error.new_line;
			io.error.putbool (has_precondition = other.has_precondition) io.error.new_line;
			io.error.putbool (has_postcondition = other.has_postcondition) io.error.new_line;
	end
end
			if Result then
				if assert_id_set /= Void then
					Result := assert_id_set.same_as (other.assert_id_set)
				else
					Result := (other.assert_id_set = Void)
				end
			end

			if Result and then rout_id_set.first = System.default_rescue_id then
				-- This is the default rescue feature.
				-- Test whether emptiness of body has changed.
				Result := (empty_body = other.empty_body)
			end

			if Result and then rout_id_set.first = System.default_create_id then
				-- This is the default create feature.
				-- Test whether emptiness of body has changed.
				Result := (empty_body = other.empty_body)
			end

debug ("ACTIVITY")
	if not Result then
		io.error.putstring ("%T%T")
		io.error.putstring (feature_name)
		io.error.putstring (" is not equiv%N")
	end
end
		end

	select_table_equiv (other: FEATURE_I): BOOLEAN is
			-- Incrementality of the select table
		require
			good_argumnet: other /= Void

		do
			Result := written_in = other.written_in
						and then rout_id_set.same_as (other.rout_id_set)
						and then is_origin = other.is_origin
						and then body_index = other.body_index
						and then type.is_deep_equal (other.type)
		end

	is_valid: BOOLEAN is
			-- Is the feature still valid?
			-- Incrementality: The types of the arguments and/or result
			-- are still defined in the system
		local
			type_a: TYPE_A
		do
			type_a ?= type
			Result := type_a.is_valid
			if Result and then has_arguments then
				Result := arguments.is_valid
			end
		end

	same_class_type (other: FEATURE_I): BOOLEAN is
			-- Has `other' the same resulting type than Current ?
		require
			good_argument: other /= Void
			same_names: other.feature_name_id = feature_name_id
		do
			Result := type.same_as (other.type)
		end

	same_interface (other: FEATURE_I): BOOLEAN is
			-- Has `other' the same interface than Current ?
			-- [Semnatic for second pass is `old_feat.same_interface (new)']
		require
			good_argument: other /= Void
			same_names: other.feature_name_id = feature_name_id
--			export_statuses_exist: not (export_status = Void
--										or else	other.export_status = Void)
		do
				-- Still an attribute
			Result := is_attribute = other.is_attribute 

				-- Same return type
			Result := Result and then type.same_as (other.type)
--						and then export_status.equiv (other.export_status)

				-- Same arguments if any
			if Result then
				if argument_count = 0 then
					Result := other.argument_count = 0
				else
					Result := argument_count = other.argument_count
							and then arguments.same_interface (other.arguments)
				end
			end

				-- Still a once
			Result := Result and then is_once = other.is_once
		end

feature -- test for empty body

	empty_body : BOOLEAN        -- Does feature have an empty body?

	set_empty_body (b : BOOLEAN) is
			-- Set `empty_body' to `b'
		do
			empty_body := b
		ensure
			set : empty_body = b
		end

feature -- creation of default rescue clause

	create_default_rescue (def_resc_name : STRING) is
			-- Create default_rescue clause if necessary
		require
			valid_feature_name : def_resc_name /= Void
		local
			my_body : like body
		do
			if not (is_attribute or is_constant or 
									is_external or is_deferred) then
				my_body := body

				if (my_body /= Void) then 
					my_body.create_default_rescue (def_resc_name)
				end
			end
		end

feature -- Type id

	written_type_id (class_type: CL_TYPE_I): INTEGER is
			-- Written type id of the feature in the context of the
			-- type `class_type'.
		do
			Result := written_type (class_type).type_id
		end

	written_type (class_type: CL_TYPE_I): CL_TYPE_I is
			-- Written type of the feature in the context of the
			-- type `class_type'.
		require
			good_argument: class_type /= Void
			conformance: class_type.base_class.conform_to (written_class)
		do
			Result := written_class.meta_type (class_type)
		end

feature -- Conveniences

	assert_id_set: ASSERT_ID_SET is
			-- Assertions to which the procedure belongs to
			-- (To be redefined in PROCEDURE_I).
		do
			-- Do nothing
		end

	is_obsolete: BOOLEAN is
			-- Is Current feature obsolete?
		do
			Result := obsolete_message /= Void
		end

	obsolete_message: STRING is
			-- Obsolete message
			-- (Void if Current is not obsolete)
		do
			-- Do nothing
		end

	has_arguments: BOOLEAN is
			-- Has the current feature some formal arguments ?
		do
			Result := arguments /= Void
		end

	is_replicated: BOOLEAN is
			-- Is Current feature conceptually replicated?
		do
			-- Do nothig
		end

	is_procedure: BOOLEAN is
			-- Is the current feature a procedure ?
		do
			-- Do nothing
		end

	is_function: BOOLEAN is
			-- Is the cuurent feature a function ?
		do
			-- Do nothing
		end

	is_attribute: BOOLEAN is
			-- Is the current feature an attribute ?
		do
			-- Do nothing
		end

	is_constant: BOOLEAN is
			-- Is the current feature a constant ?
		do
			-- Do nothing
		end

	is_once: BOOLEAN is
			-- Is the current feature a once one ?
		do
			-- Do nothing
		end

	is_do: BOOLEAN is
			-- Is the current feature a do one ?
		do
			-- Do nothing
		end

	is_deferred: BOOLEAN is
			-- Is the current feature a deferred one ?
		do
			-- Do nothing
		end

	is_unique: BOOLEAN is
			-- Is the current feature a unique constant ?
		do
			-- Do nothing
		end

	is_external: BOOLEAN is
			-- Is the current feature an external one ?
		do
			-- Do nothing
		end

	frozen is_c_external: BOOLEAN is
			-- Is current feature a C external one?
		local
			ext: EXTERNAL_I
		do
			if is_external then
				ext ?= Current
				Result := not ext.extension.is_il
			end
		end

	is_require_else: BOOLEAN is
			-- Is the precondition block of the feature a redefined one ?
		do
			Result := True
		end

	has_static_access: BOOLEAN is
			-- Can Current be access in a static manner?
		local
			ext: EXTERNAL_I
			att: ATTRIBUTE_I
			extension: IL_EXTENSION_I
		do
			if System.il_generation then
				ext ?= Current
				if ext /= Void then
					extension ?= ext.extension
				else
					att ?= Current
					if att /= Void then
						extension ?= att.extension
					end
				end
				Result := (extension /= Void and then not extension.need_current (extension.type)) or
					(is_constant and then not is_once)
			else
				Result := is_external or (is_constant and then not is_once)
			end			
		end
		
	has_precondition: BOOLEAN is
			-- Is the feature declaring some preconditions ?
		do
			-- Do nothing
		end

	has_postcondition: BOOLEAN is
			-- Is the feature declaring some postconditions ?
		do
			-- Do nothing
		end

	has_assertion: BOOLEAN is
			-- Is the feature declaring some pre or post conditions ?
		do
			Result := has_postcondition or else has_precondition
		end

	is_ensure_then: BOOLEAN is
			-- Is the postcondition block of the feature a redefined one ?
		do
			Result := True
		end

	can_be_encapsulated: BOOLEAN is
			-- Is current feature a feature that can be encapsulated?
			-- Eg: attribute or constant.
		do
		end

	redefinable: BOOLEAN is
			-- Is the feature redefinable ?
		do
			Result := not is_frozen
		end

	undefinable: BOOLEAN is
			-- Is the feature undefinable ?
		do
			Result := redefinable
		end

	is_none_attribute: BOOLEAN is
			-- is the feature an attribute of type NONE ?
		do
			-- Do nothing
		end

	type: TYPE is
			-- Type of the feature
		do
			Result := Void_type
		end

	set_type (t: TYPE) is
			-- Assign `t' to `type'.
		do
			-- Do nothing
		end

	arguments: FEAT_ARG is
			-- Argument types
		do
			-- No arguments
		end

	set_assert_id_set (set: like assert_id_set) is
			-- Assign `set' to assert_id_set.
		do
			-- Do nothing	
		end

	argument_count: INTEGER is
			-- Number of arguments of the feature
		do
			if arguments /= Void then
				Result := arguments.count
			end
		end

	written_class: CLASS_C is
			-- Class where the feature is written in
		require
			good_written_in: written_in /= 0
		do
			Result := System.class_of_id (written_in)
		end

feature -- Export checking

--	has_special_export: BOOLEAN is
--			-- The export status is special, i.e. the feature
--			-- is not exported to all the other classes.
--			-- A call to this feature must be recorded specially
--			-- in the dependances for incrementality purpose:
--			-- If the hierarchy changes, the call may be invalid.
--		require
--			has_export_status: export_status /= Void
--		do
--			Result := not export_status.is_all
--		end

	is_exported_for (client: CLASS_C): BOOLEAN is
			-- Is the current feature exported to class `client' ?
		require
			good_argument: client /= Void
			has_export_status: export_status /= Void
		do
			Result := export_status.valid_for (client)
		end

	record_suppliers (feat_depend: FEATURE_DEPENDANCE) is
			-- Record the suppliers ids in `feat_depend'
		require
			good_arg: feat_depend /= Void
		local
			type_a: TYPE_A
			a_class: CLASS_C
		do
				-- Create the supplier set for the feature
			type_a ?= type
			if type_a /= Void then
				a_class := type_a.associated_class
				if a_class /= Void then
					feat_depend.add_supplier (a_class)
				end
				type_a.update_dependance (feat_depend)
			end
			if has_arguments then
				from
					arguments.start
				until
					arguments.after
				loop
					type_a ?= arguments.item
					a_class := type_a.associated_class
					if a_class /= Void then
						feat_depend.add_supplier (a_class)
					end
					arguments.forth
				end
			end
		end

	suppliers: TWO_WAY_SORTED_SET [INTEGER] is
			-- Class ids of all the suppliers of the feature
		require
			Tmp_depend_server.has (written_in) or else
			Depend_server.has (written_in)
		local
			class_dependance: CLASS_DEPENDANCE
		do
			if Tmp_depend_server.has (written_in) then
				class_dependance := Tmp_depend_server.item (written_in)
			else
				class_dependance := Depend_server.item (written_in)
			end
			Result := class_dependance.item (body_index).suppliers
		end

feature -- Check

-- Note: `require else' can be used even if the feature has no
-- precursor. There is no problem to raise an error in the normal case,
-- the only case  where we cannot do anything is when aliases are used
-- and one name references a feature with a predecessor and not the
-- other one

--	check_assertions is
--			-- Raise an error if "require else" or "ensure then" is used
--			-- but the feature has no ancestor
--		do
--		end

	body: FEATURE_AS is
			-- Body of the feature
		local
			class_ast: CLASS_AS
			bid: INTEGER
		do
			if body_index > 0 then
				bid := body_index
				if Tmp_body_server.has (bid) then
					Result := Tmp_body_server.item (bid)
				elseif Body_server.server_has (bid) then
					Result := Body_server.server_item (bid)
				end
			end
			if Result = Void then
				if Tmp_ast_server.has (written_in) then
					-- Means a degree 4 error has occurred so the
					-- best we can do is to search through the
					-- class ast and find the feature as
					class_ast := Tmp_ast_server.item (written_in)
					Result ?= class_ast.feature_with_name (feature_name)
				end
			end
		end

	type_check is
			-- Third pass on current feature
		require
			in_pass3
		do
-- See the note near the declaration of `check_assertions'
--			check_assertions
			record_suppliers (context.supplier_ids)
				-- Take the body in the body server
debug ("SERVER", "TYPE_CHECK")
	io.error.putstring ("feature name: ")
	io.error.putstring (feature_name)
	io.error.new_line
	io.error.putstring ("body index: ")
	io.error.putint (body_index)
	io.error.new_line
end
				-- make the type check
			body.type_check
		end

	check_local_names is
			-- Check the conflicts between local names and feature names
			-- for an unchanged feature
		do
		end

	in_pass3: BOOLEAN is
			-- Does the current feature support the type check ?
		do
			Result := True
		end

feature -- IL code generation

	generate_il is
			-- Generate IL code for current feature.
		local
			byte_code: BYTE_CODE
		do
			if not is_attribute and then not is_external then
				byte_code := Byte_server.item (body_index)
				byte_context.set_byte_code (byte_code)
				byte_code.generate_il
				byte_context.clear_all
			end
		end

feature -- Byte code computation

	compute_byte_code (has_default_rescue: BOOLEAN) is
			-- Compute byte code for melted feature
		require
			in_pass3: in_pass3
		local
			byte_code: BYTE_CODE
		do
				-- Process byte code
			byte_code := body.byte_node
			byte_code.set_default_rescue (has_default_rescue)

				-- Put it in the temporary byte code server
			if not byte_context.old_expressions.is_empty then
				byte_code.set_old_expressions (byte_context.old_expressions)
			end
			byte_context.clear_old_expressions

			Tmp_byte_server.put (byte_code)
		end

	melt (exec: EXECUTION_UNIT) is
			-- Generate byte code for the current feature
			-- [To be redefined in CONSTANT_I, ATTRIBUTE_I and in EXTERNAL_I].
		require
			good_argument: exec /= Void
		local
			byte_code: BYTE_CODE
			melted_feature: MELT_FEATURE
		do
			byte_code := Byte_server.item (body_index)

			byte_context.set_byte_code (byte_code)
			byte_context.set_current_feature (Current)

			Byte_array.clear
			byte_code.set_real_body_id (real_body_id)
			byte_code.make_byte_code (Byte_array)
			byte_context.clear_all

			melted_feature := Byte_array.melted_feature
			melted_feature.set_real_body_id (exec.real_body_id)
	
			if not System.freeze then
				Tmp_m_feature_server.put (melted_feature)
			end

			Execution_table.mark_melted (exec)
		end

feature -- Polymorphism

 	has_entry: BOOLEAN is
 			-- Has the feature an associated polymorphic unit ?
 		do
 			Result := True
 		end
 
 	new_entry (rout_id: INTEGER): ENTRY is
 			-- New polymorphic unit
 		require
			rout_id_not_void: rout_id /= 0
 		do
 			if not is_attribute or not Routine_id_counter.is_attribute (rout_id) then
 				Result := new_rout_entry
 			else
 				Result := new_attr_entry
 			end
 		end
 
 	new_rout_entry: ROUT_ENTRY is
 			-- New routine unit
 		require
 			not_deferred: not is_deferred
 		do
 			!!Result
 			Result.set_body_index (body_index)
 			Result.set_type_a (type.actual_type)
 			Result.set_written_in (written_in)
 			Result.set_pattern_id (pattern_id)
			Result.set_feature_id (feature_id)
 		end
 
 	new_attr_entry: ATTR_ENTRY is
 			-- New attribute unit
 		require
 			is_attribute: is_attribute
 		do
 			!!Result
 			Result.set_type_a (type.actual_type)
 			Result.set_feature_id (feature_id)
 		end
 
 	poly_equiv (other: FEATURE_I): BOOLEAN is
 			-- Is `other' equivalent to Current from the polymorhic table 
			-- implementation point of view ?
 		require
 			good_argument: other /= Void
 		local
 			is_attr: BOOLEAN
 		do
 			is_attr := is_attribute
 			Result := 	other.is_attribute = is_attr
 						and then
 						type.actual_type.same_as (other.type.actual_type)
 			if Result then
 				if is_attr then
 					Result := 	feature_id = other.feature_id
				else
	 				Result :=   written_in = other.written_in
 								and then body_index = other.body_index
 								and then pattern_id = other.pattern_id
 				end
 			end
 		end

feature -- Signature instantiation

	instantiate (parent_type: CL_TYPE_A) is
			-- Instantiated the signature in the context of `parent_type'.
		require
			good_argument: parent_type /= Void
			is_solved: type.is_solved
		local
			i, nb: INTEGER
			old_type: TYPE_A
		do
				-- Instantiation of the type
			old_type ?= type
			set_type (old_type.instantiated_in (parent_type))
				-- Instantiation of the arguments
			from
				i := 1
				nb := argument_count
			until
				i > nb
			loop
				old_type ?= arguments.i_th (i)
				arguments.put_i_th (old_type.instantiated_in (parent_type), i)
				i := i + 1
			end
		end

feature -- Signature checking
	
	check_argument_names (feat_table: FEATURE_TABLE) is
			-- Check the argument names
		require
			argument_names_exists: arguments.argument_names /= Void
			written_in_class: written_in = feat_table.feat_tbl_id
				-- The feature must be written in the associated class
				-- of `feat_table'.
		local
			args: like arguments
			arg_id: INTEGER
			vreg: VREG
			vrfa: VRFA
			i, nb: INTEGER
		do
			from
				args := arguments
				i := 1
				nb := args.count
			until
				i > nb
			loop
				arg_id := args.item_id (i)
					-- Searching to find after the current item another one
					-- with the same name. 
					-- We do `i + 1' for the start index because we need to go
					-- one step further (+ 1)
				if args.argument_position_id (arg_id, i + 1) /= 0 then
						-- Two arguments with the same name
					create vreg
					vreg.set_class (written_class)
					vreg.set_feature (Current)
					vreg.set_entity_name (Names_heap.item (arg_id))
					Error_handler.insert_error (vreg)
				end
				if feat_table.has_id (arg_id) then
						-- An argument name is a feature name of the feature
						-- table.
					create vrfa
					vrfa.set_class (written_class)
					vrfa.set_feature (Current)
					vrfa.set_other_feature (feat_table.found_item)
					Error_handler.insert_error (vrfa)
				end
				i := i + 1
			end
		end

	check_types (feat_table: FEATURE_TABLE) is
			-- Check the type and the arguments types. The objective is
			-- to deal with anchored types and genericity. All the anchored
			-- types are interpreted here and the generic parameter
			-- instantiated if possible.
		require
			type /= Void
		local
			solved_type: TYPE_A
			vffd5: VFFD5
			vffd6: VFFD6
			vffd7: VFFD7
			vtug: VTUG
			vtcg1: VTCG1
		do
			if type.has_like and then is_once then
					-- We have an anchored type.
					-- Check if the feature is not a once feature
				!!vffd7
				vffd7.set_class (written_class)
				vffd7.set_feature_name (feature_name)
				Error_handler.insert_error (vffd7)
			end
				-- Process an actual type for the feature interpret
				-- anchored types.
			solved_type := Result_evaluator.evaluated_type
												(type, feat_table, Current)

			check
					-- If an anchored cannot be valuated then an
					-- exection is triggered by the type evaluator.
				solved_type /= Void
			end
debug ("ACTIVITY")
	io.error.putstring ("Check types of ")
	io.error.putstring (feature_name)
	io.error.new_line
	if solved_type = Void then
		io.error.putstring ("VOID solved type!!%N")
	else
		io.error.putstring ("Solved type: ")
		io.error.putstring (solved_type.dump)
		io.error.new_line
	end
end

			if feat_table.associated_class = written_class then
					-- Check valididty of a generic class type
				if not solved_type.good_generics then
					vtug := solved_type.error_generics
					vtug.set_class (written_class)
					vtug.set_feature (Current)
					vtug.set_entity_name ("Result")
					Error_handler.insert_error (vtug)
						-- Cannot go on here ..
					Error_handler.raise_error
				end
					-- Check constrained genericity validity rule
				solved_type.reset_constraint_error_list
				solved_type.check_constraints (written_class)
				if not solved_type.constraint_error_list.is_empty then
					!!vtcg1
					vtcg1.set_class (written_class)
					vtcg1.set_feature (Current)
					vtcg1.set_error_list (solved_type.constraint_error_list)
					Error_handler.insert_error (vtcg1)
				end
			end

			set_type (solved_type)
				-- Instantitate the feature type in the context of the
				-- actual type of the class associated to `feat_table'.

			if is_once and then solved_type.has_formal_generic then
					-- A once funtion cannot have a type with formal generics
				!!vffd7
				vffd7.set_class (written_class)
				vffd7.set_feature_name (feature_name)
				Error_handler.insert_error (vffd7)
			end
			solved_type.check_for_obsolete_class (feat_table.associated_class)

			if 
				is_infix and then 
				((argument_count /= 1) or else (type = Void_type))
			then
					-- Infixed features should have only one argument
					-- and must have a return type.
				!!vffd6
				vffd6.set_class (written_class)
				vffd6.set_feature_name (feature_name)
				Error_handler.insert_error (vffd6)
			end
			if 
				is_prefix and then 
				((argument_count /= 0) or else (type = Void_type))
			then
					-- Prefixed features shouldn't have any argument
					-- and must have a return type.
				!!vffd5
				vffd5.set_class (written_class)
				vffd5.set_feature_name (feature_name)
				Error_handler.insert_error (vffd5)
			end

			if arguments /= Void then
					-- Check types of arguments
				arguments.check_types (feat_table, Current)
			end
		end

	check_expanded (class_c: CLASS_C) is
			-- Check the expanded validity rules
		require
			type /= Void
		local
			solved_type: TYPE_A
			vtec1: VTEC1
			vtec2: VTEC2
		do
debug ("CHECK_EXPANDED")
	io.error.putstring ("Check expanded of ")
	io.error.putstring (feature_name)
	io.error.new_line
end
			if class_c.class_id = written_in then
					-- Check validity of an expanded result type

					-- `set_type' has been called in `check_types' so
					-- the reverse assignment is valid.
				solved_type ?= type
				if	solved_type.has_expanded then
					if 	solved_type.expanded_deferred then
						!!vtec1
						vtec1.set_class (written_class)	
						vtec1.set_feature (Current)
						vtec1.set_entity_name (feature_name)
						Error_handler.insert_error (vtec1)
					elseif not solved_type.valid_expanded_creation (class_c) then
						!!vtec2
						vtec2.set_class (written_class)	
						vtec2.set_feature (Current)
						vtec2.set_entity_name (feature_name)
						Error_handler.insert_error (vtec2)
					end
				end
				if solved_type.has_generics then
					system.expanded_checker.check_actual_type (solved_type)
				end
				if arguments /= Void then
					arguments.check_expanded (class_c, Current)
				end
			end
		end

	check_signature (old_feature: FEATURE_I) is
			-- Check the signature conformance beetween Current
			-- and inherited feature in `inherit_info' from which Current
			-- is a redefinition.
		require
			good_argument: old_feature /= Void
		local
			old_type, new_type: TYPE_A
			i, arg_count: INTEGER
			new_ext, old_ext: EXTERNAL_I
			new_extension, old_extension: CPP_EXTENSION_I
			old_arguments: like arguments
			current_class: CLASS_C
			vdrd51: VDRD51
			vdrd52: VDRD52
			vdrd53: VDRD53
			vdrd6: VDRD6
			vdrd7: VDRD7
			vdrd71: VDRD71
			ve01: VE01
			ve02: VE02
			ve02a: VE02A
		do
debug ("ACTIVITY")
	io.error.putstring ("Check signature of ")
	io.error.putstring (feature_name)
	io.error.new_line
end
			current_class := System.current_class

				-- Check if an attribute is redefined in an attribute
			if old_feature.is_attribute and then not is_attribute then
				!!vdrd6
				vdrd6.init (old_feature, Current)
				Error_handler.insert_error (vdrd6)
			end


			if not System.il_generation then
					-- Check if an external (resp. non-external) feature is not
					-- redefined into a non external (resp. external) one
				if old_feature.is_external /= is_external then
					!!ve01
					ve01.set_class (current_class)
					ve01.set_feature (Current)
					ve01.set_old_feature (old_feature)
					Error_handler.insert_error (ve01)
				elseif is_external then
					old_ext ?= old_feature
					new_ext ?= Current
						-- This was the previous implementation:
						-- new_ext.set_encapsulated
						--			  (new_ext.encapsulated or else old_ext.encapsulated)
						-- In the case where a class redefines a routine that 
						-- requires encapsulation (is_special or has_signature is True)
						-- into a `standard' external that doesn't need to be encapsulated
						-- the previous implementation led this `standard' external to
						-- be encapsulated. Bad. -- Fabrice.

						-- C++ redeclaration
					old_extension ?= old_ext.extension
					new_extension ?= new_ext.extension

					if
						(old_extension /= Void) /= (new_extension /= Void)
						or else
						((new_extension /= Void) and then new_extension.type /= old_extension.type)
					then
							-- Error C++ => C or C => C++
						!! vdrd71
						vdrd71.init (old_feature, Current)
						Error_handler.insert_error (vdrd71)
					end
				end
			end

				-- Check if an effective feature is not redefined in a
				-- non-effective feature
			if (not old_feature.is_deferred) and then is_deferred then
				!!vdrd7
				vdrd7.set_class (current_class)
				vdrd7.init (old_feature, Current)
				Error_handler.insert_error (vdrd7)
			end
	
				-- Initialization for like-argument types
			Argument_types.init1 (Current)
		
			old_type ?= old_feature.type	
			old_type := old_type.conformance_type.actual_type
				-- `new_type' is the actual type of the redefinition already
				-- instantiated
			new_type := type.actual_type
debug ("ACTIVITY")
	io.error.putstring ("Types:%N")
	if old_type /= Void then
		io.error.putstring ("old type:%N")
		io.error.putstring (old_type.dump)
		io.error.new_line
	end
	if new_type /= Void then
		io.error.putstring ("new type:%N")
		io.error.putstring (new_type.dump)
		io.error.new_line
	else
		io.error.putstring ("New type: VOID%Ntype:")
		io.error.putstring (type.dump)
		io.error.new_line
		io.error.putstring (type.out)
		io.error.new_line
	end
	io.error.new_line
end

			if not current_class.valid_redeclaration (old_type, new_type) then
				!!vdrd51
				vdrd51.init (old_feature, Current)
				Error_handler.insert_error (vdrd51)
			elseif
				new_type.is_true_expanded /= old_type.is_true_expanded
			then
				!!ve02
				ve02.init (old_feature, Current)
--				ve02.set_type (new_type)
--				ve02.set_precursor_type (old_type)
				Error_handler.insert_error (ve02)
			end

				-- Check the argument count
			if argument_count /= old_feature.argument_count then
				!!vdrd52
				vdrd52.init (old_feature, Current)
				Error_handler.insert_error (vdrd52)
			else
					-- Check the argument conformance
				from
					i := 1
					arg_count := argument_count
					old_arguments := old_feature.arguments
				until
					i > arg_count
				loop
					old_type ?= old_arguments.i_th (i)
					old_type := old_type.conformance_type.actual_type
					new_type := arguments.i_th (i).actual_type
debug ("ACTIVITY")
	io.error.putstring ("Types:%N")
	if old_type /= Void then
		io.error.putstring ("old type:%N")
		io.error.putstring (old_type.dump)
		io.error.new_line
	end
	if new_type /= Void then
		io.error.putstring ("new type:%N")
		io.error.putstring (new_type.dump)
		io.error.new_line
	end
	io.error.new_line
end
					if not
						current_class.valid_redeclaration (old_type, new_type)
					then
						!!vdrd53
						vdrd53.init (old_feature, Current)
						Error_handler.insert_error (vdrd53)
					elseif
						new_type.is_true_expanded /= old_type.is_true_expanded
					then
						!!ve02a
						ve02a.init (old_feature, Current)
--						ve02a.set_type (new_type)
--						ve02a.set_precursor_type (old_type)
						ve02a.set_argument_number (i)
						Error_handler.insert_error (ve02a)
					end
	
					i := i + 1
				end
			end
		end

	check_same_signature (old_feature: FEATURE_I) is
			-- Check the signature equality beetween Current
			-- and inherited feature in `inherit_info' from which Current
			-- is a join.
		require
			good_argument: old_feature /= Void
			is_deferred
			old_feature.is_deferred
			old_feature_is_solved: old_feature.type.is_solved
		local
			old_type, new_type: TYPE_A
			i, arg_count: INTEGER
			old_arguments: like arguments
			vdjr: VDJR
			vdjr1: VDJR1
			vdjr2: VDJR2
		do
				-- Check the result type conformance
				-- `old_type' is the instantiated inherited type in the
				-- context of the class where the join takes place:
				-- i.e the class relative to `written_in'.
			old_type ?= old_feature.type
				-- `new_type' is the actual type of the join already
				-- instantiated
			new_type ?= type
			if not new_type.is_deep_equal (old_type) then
				!!vdjr1
				vdjr1.init (old_feature, Current)
				vdjr1.set_type (new_type)
				vdjr1.set_old_type (old_type)
				Error_handler.insert_error (vdjr1)
			end

				-- Check the argument count
			if argument_count /= old_feature.argument_count then
				!!vdjr
				vdjr.init (old_feature, Current)
				Error_handler.insert_error (vdjr)
			else
	
					-- Check the argument equality
				from
					i := 1
					arg_count := argument_count
					old_arguments := old_feature.arguments
				until
					i > arg_count
				loop
					old_type ?= old_arguments.i_th (i)
					new_type ?= arguments.i_th (i)
					if not new_type.is_deep_equal (old_type) then
						!!vdjr2
						vdjr2.init (old_feature, Current)
						vdjr2.set_type (new_type)
						vdjr2.set_old_type (old_type)
						vdjr2.set_argument_number (i)
						Error_handler.insert_error (vdjr2)
					end
					i := i + 1
				end
			end
		end

	has_same_il_signature (a_parent_type, a_written_type: CL_TYPE_A; old_feature: FEATURE_I): BOOLEAN is
			-- Is current feature defined in `a_written_type' same as `old_feature'
			-- defined in `a_parent_type'?
		require
			a_parent_type_not_void: a_parent_type /= Void
			a_written_type_not_void: a_written_type /= Void
			old_feature_not_void: old_feature /= Void
			il_generation: System.il_generation
		local
			old_type, new_type: TYPE_A
			i, arg_count, id: INTEGER
			old_arguments: like arguments
		do
				-- Initialization for like-argument types
			Argument_types.init1 (Current)
	
			id := a_written_type.base_class_id
			old_type ?= old_feature.type	
			old_type := old_type.conformance_type.
				instantiation_in (a_written_type, a_written_type.base_class_id).actual_type

			new_type := type.actual_type

				-- Check exact match of signature for IL generation.
			Result := old_type.same_as (new_type)

				-- Check the argument conformance
			from
				i := 1
				arg_count := argument_count
				old_arguments := old_feature.arguments
			until
				i > arg_count
			loop
				old_type ?= old_arguments.i_th (i)
				old_type := old_type.conformance_type.
					instantiation_in (a_written_type, a_written_type.base_class_id).actual_type

				new_type := arguments.i_th (i).actual_type
					-- Check exact match of signature for IL generation.
				Result := Result and then old_type.same_as (new_type)

				i := i + 1
			end
		end

	solve_types (feat_tbl: FEATURE_TABLE) is
			-- Evaluates signature types in the context of `feat_tbl'.
			-- | Take care of possible anchored types
		do
			set_type
				(Result_evaluator.evaluated_type (type, feat_tbl, Current))
			if arguments /= Void then
				arguments.solve_types (feat_tbl, Current)
			end
		end

	same_signature (other: FEATURE_I): BOOLEAN is
			-- Has `other' the same signature than Current ?
		require
			good_argument: other /= Void
			same_feature_names: feature_name_id = other.feature_name_id
		local
			i, nb: INTEGER
		do
			Result := type.is_deep_equal (other.type)
			from
				nb := argument_count
				Result := Result and then nb = other.argument_count
				nb := argument_count
				i := 1
			until
				i > nb or else not Result
			loop
				Result := arguments.i_th (i).is_deep_equal (other.arguments.i_th (i))
				i := i + 1
			end
		end

	argument_position (arg_id: ID_AS): INTEGER is
			-- Position of argument `arg_id' in list of arguments
			-- of current feature. 0 if none or not found.
		require
			arg_id /= Void
		do
			if arguments /= Void then
				Result := arguments.argument_position (arg_id, 1)
			end
		end

	has_argument_name (arg_id: INTEGER): BOOLEAN is
			-- Has current feature an argument named `arg_id" ?
		require
			arg_id /= Void
		do
			if arguments /= Void then
				Result := arguments.argument_position_id (arg_id, 1) /= 0
			end
		end

feature -- Undefinition

	new_deferred: DEF_PROC_I is
			-- New deferred feature for undefinition
		require
			not is_deferred
			redefinable
		do
			if is_function then
				create {DEF_FUNC_I} Result
			else
				create Result
			end
			Result.set_type (type)
			Result.set_arguments (arguments)
			Result.set_original_name_id (original_name_id)
			Result.set_written_in (written_in)
			Result.set_implemented_in (implemented_in)
			Result.set_belongs_to_interface (True)
			Result.set_rout_id_set (rout_id_set)
			Result.set_assert_id_set (assert_id_set)
			Result.set_is_selected (is_selected)
			Result.set_is_infix (is_infix)
			Result.set_is_prefix (is_prefix)
			Result.set_is_frozen (is_frozen)
			Result.set_feature_name_id (feature_name_id)
			Result.set_feature_id (feature_id)
			Result.set_pattern_id (pattern_id)
			Result.set_is_require_else (is_require_else)
			Result.set_is_ensure_then (is_ensure_then)
			Result.set_export_status (export_status)
			Result.set_body_index (body_index)
			Result.set_has_postcondition (has_postcondition)
			Result.set_has_precondition (has_precondition)
		ensure
			Result_exists: Result /= Void
			Result_is_deferred: Result.is_deferred
		end

	new_rout_id: INTEGER is
			-- New routine id
		do
			Result := Routine_id_counter.next_rout_id
		end

	Routine_id_counter: ROUTINE_COUNTER is
			-- Routine id counter
		once
			Result := System.routine_id_counter
		end

feature -- Replication

	code_id: INTEGER is
			-- Code id for inheritance analysis
		do
			Result := body_index
		end

	set_code_id (i: INTEGER) is
			-- Assign `i' to code_id.
		do
			-- Do nothing
		end

	access_in: INTEGER is
			-- Id of the class where the current feature can be accessed
			-- through its routine id
			-- Useful for replication
		do
			Result := written_in
		end

	replicated: FEATURE_I is
			-- Replicated feature
		deferred
		ensure
			Result /= Void
		end

	new_code_id: INTEGER is
			-- New code id
		do
			Result := System.body_index_counter.next_id
		end

	unselected (in: INTEGER): FEATURE_I is
			-- Unselected feature
		require
			in_not_void: in /= 0
		deferred
		ensure
			Result_exists: Result /= Void
		end

	is_unselected: BOOLEAN is
			-- Is the current feature an unselected one ?
		do
			-- Do nothing
		end

	transfer_to (other: FEATURE_I) is
			-- Transfer of datas from Current into `other'.
		require
			other_exists: other /= Void
		do
			other.set_body_index (body_index)
			other.set_export_status (export_status)
			other.set_feature_id (feature_id)
			other.set_feature_name_id (feature_name_id)
			other.set_is_frozen (is_frozen)
			other.set_is_infix (is_infix)
			other.set_is_prefix (is_prefix)
			other.set_is_selected (is_selected)
			other.set_pattern_id (pattern_id)
			other.set_rout_id_set (rout_id_set)
			other.set_written_in (written_in)
			other.set_original_name_id (original_name_id)
			other.set_implemented_in (implemented_in)
			other.set_belongs_to_interface (belongs_to_interface)
			other.set_is_origin (is_origin)
		end

feature -- Genericity

	update_instantiator2 (a_class: CLASS_C) is
			-- Look for generic types in the result and arguments in order
			-- to update the instantiator.
		require
			good_argument: a_class /= Void
			good_context: a_class.changed
		local
			gen_type: GEN_TYPE_A
			i, arg_count: INTEGER
		do
			gen_type ?= type.actual_type
			if gen_type /= Void then
					-- Found generic result type
				Instantiator.dispatch (gen_type, a_class)
			end
			from
				i := 1
				arg_count := argument_count
			until
				i > arg_count
			loop	
				gen_type ?= arguments.i_th (i).actual_type
				if gen_type /= Void then
						-- Found generic argument type
					Instantiator.dispatch (gen_type, a_class)
				end
				i := i + 1
			end
		end

feature -- Pattern

	pattern: PATTERN is
			-- Feature pattern
		do
			!!Result.make (type.actual_type.meta_type)
			if argument_count > 0 then
				Result.set_argument_types (arguments.pattern_types)
			end
		end

	process_pattern is
			-- Process pattern of Current feature
		local
			p: PATTERN_TABLE
		do
			p := Pattern_table
			p.insert (generation_class_id, pattern)
			pattern_id := p.last_pattern_id
		end
			
feature -- Dead code removal

	used: BOOLEAN is
			-- Is the feature used ?
		do
					-- In final mode dead code removal process is on.
					-- In workbench mode all the features are considered
					-- used.
			Result := 	byte_context.workbench_mode 
						or else
						System.is_used (Current)
		end

feature -- Byte code access

	access (access_type: TYPE_I): ACCESS_B is
			-- Byte code access for current feature
		local
			feature_b: FEATURE_B
			feature_bs: FEATURE_BS
			last_constrained_type: TYPE_A
		do
			last_constrained_type := context.last_constrained_type
			if (last_constrained_type /= Void and then last_constrained_type.is_separate) then
				!!feature_bs
				feature_bs.init (Current)
				feature_bs.set_type (access_type)
				Result := feature_bs
			else
				!!feature_b
				feature_b.init (Current)
				feature_b.set_type (access_type)
				Result := feature_b
			end
		ensure
			Result_exists: Result /= Void
		end

feature {NONE} -- log file

	add_in_log (class_type: CLASS_TYPE; encoded_name: STRING) is
		do
			System.used_features_log_file.add (class_type, feature_name, encoded_name)
		end

feature -- C code generation

	generate (class_type: CLASS_TYPE; buffer: GENERATION_BUFFER) is
			-- Generate feature written in `class_type' in `buffer'.
		require
			valid_buffer: buffer /= Void
			written_in_type: class_type.associated_class.class_id = generation_class_id
			not_deferred: not is_deferred
		local
			byte_code: BYTE_CODE
			tmp_body_index: INTEGER
		do
			if used then
					-- `generate' from BYTE_CODE will log the feature name
					-- and encoded name in `used_features_log_file' from SYSTEM_I
				generate_header (buffer)

				tmp_body_index := body_index
				if Tmp_opt_byte_server.has (tmp_body_index) then
					byte_code := Tmp_opt_byte_server.disk_item (tmp_body_index)
				else
					byte_code := Byte_server.disk_item (tmp_body_index)
				end

					-- Generation of C code for an Eiffel feature written in
					-- the associated class of the current type.
				byte_context.set_byte_code (byte_code)

				if System.in_final_mode and then System.inlining_on then
					byte_code := byte_code.inlined_byte_code
				end

					-- Generation of the C routine
				byte_context.set_current_feature (Current)
				byte_code.analyze
				byte_code.set_real_body_id (real_body_id)
				byte_code.generate
				byte_context.clear_all

			else
				System.removed_log_file.add (class_type, feature_name)
			end
		end

	generate_header (buffer: GENERATION_BUFFER) is
			-- Generate a header before the body of the feature
		require
			valid_buffer: buffer /= Void
		do
			buffer.putstring ("/* ")
			buffer.putstring (feature_name)
			buffer.putstring (" */%N%N")
		end

feature -- Debug purpose

	trace is
			-- Debug purpose
		do
			io.error.putstring ("feature name: ")
			io.error.putstring (feature_name)
			io.error.putchar (' ')
			rout_id_set.trace
			io.error.putstring (" {")
			io.error.putstring ("fid = ")
			io.error.putint (feature_id)
			io.error.putstring ("}");
			io.error.putstring (" {")
			io.error.putstring ("body_index = ")
			io.error.putint (body_index)
			io.error.putstring ("}");
			io.error.putstring (" {")
			io.error.putstring ("written in = ")
			io.error.putstring (written_class.name)
			io.error.putstring ("}");
			io.error.putstring (" {")
			io.error.putstring ("body_index = ")
			if body_index > 0 then
				io.error.putint (body_index)
			else
				io.error.putint (0)
			end
			io.error.new_line
		end

feature -- Debugging

	is_debuggable: BOOLEAN is
		local
			wc: CLASS_C
		do
			if (not is_external)
				and then (not is_attribute)
				and then (not is_constant)
				and then (not is_deferred)
				and then (not is_unique)
			then
				wc := written_class
				Result := (not wc.is_basic)
					and then (not wc.is_special)
					and then (wc.has_types)
			end
		end

	real_body_id: INTEGER is
			-- Real body id at compilation time. This id might be
			-- obsolete after supermelting this feature.
			--| In the latter case, the new real body id is kept
			--| in DEBUGGABLE objects.
		require
			valid_body_id: valid_body_id
		local
			exec_unit: EXECUTION_UNIT
			class_type: CLASS_TYPE
			old_cluster: CLUSTER_I
		do
			old_cluster := Inst_context.cluster
			Inst_context.set_cluster (written_class.cluster)
			class_type := written_class.types.first

				-- Search for associated EXECUTION_UNIT
			create exec_unit.make (class_type)
			exec_unit.set_body_index (body_index)
			Execution_table.search (exec_unit)
			exec_unit := Execution_table.last_unit

			Result := exec_unit.real_body_id
			Inst_context.set_cluster (old_cluster)
		end

	valid_body_id: BOOLEAN is
			-- the use of this routine as precondition for real_body_id
			-- allows the enhancement of the external functions
			-- Indeed, if an external has to be encapsulated (macro, signature)
			-- an EXECUTION_UNIT is created instead of an EXT_EXECUTION_UNIT
		do
			Result := (	(not is_external)
						and then (not is_attribute)
						and then (not is_constant)
						and then (not is_deferred)
						and then (not is_unique)
						and then written_class.has_types)
				or else
					(is_constant and is_once)
		end

feature -- Api creation

	e_feature: E_FEATURE is
		do
			Result := api_feature (written_in)
		end

	api_feature (a_class_id: INTEGER): E_FEATURE is
			-- API representation of Current
		require
			a_class_id_positive: a_class_id > 0
		do
			Result := new_api_feature
			Result.set_written_in (written_in)
			Result.set_associated_class_id (a_class_id)
			Result.set_body_index (body_index)
			Result.set_is_origin (is_origin)
			Result.set_export_status (export_status)
			Result.set_is_frozen (is_frozen)
			Result.set_is_infix (is_infix)
			Result.set_is_prefix (is_prefix)
			Result.set_rout_id_set (rout_id_set)
		end		

feature {NONE} -- Implementation

	new_api_feature: E_FEATURE is
			-- API feature creation
		deferred
		ensure
			non_void_result: Result /= Void
		end

feature {INHERIT_TABLE} -- Access

	private_external_name_id: INTEGER
			-- External name id of feature if any in IL generation.

	private_external_name: STRING is
			-- External name of feature if any in IL generation.
		do
			Result := Names_heap.item (private_external_name_id)
		ensure
			Result_not_void: Result /= Void
			Result_not_empty: not Result.is_empty

		end

feature -- Concurrent Eiffel

	sep_process_pattern: BOOLEAN is
			-- Process pattern of Current feature
		local
			p: PATTERN_TABLE
		do
			p := Pattern_table
			Result := p.sep_insert (generation_class_id, pattern)
			pattern_id := p.last_pattern_id
debug ("SEP_DEBUG")
if Result then
io.putstring ("* Inserted a new pattern whose id is: ")
io.putstring (" it's pattern_id: ")
io.putint (pattern_id)
io.new_line
end
end
		end

end
