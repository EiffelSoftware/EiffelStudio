indexing
	description: "Objects that represent an EV_DIALOG.%
		%The original version of this class was generated by EiffelBuild."
	author: ""
	date: "$Date$"
	revision: "$Revision$"

class
	GB_TIP_OF_THE_DAY_DIALOG

inherit
	GB_TIP_OF_THE_DAY_DIALOG_IMP
		undefine
			png_location
		end
	
	GB_SHARED_PREFERENCES
		undefine
			default_create, copy
		end
		
	GB_SHARED_PIXMAPS
		rename
			implementation as shared_pixmaps_implementation
		undefine
			default_create, copy
		end
		
	GB_TIPS
		undefine
			default_create, copy
		end

feature {NONE} -- Initialization

	user_initialization is
			-- called by `initialize'.
			-- Any custom user initialization that
			-- could not be performed in `initialize',
			-- (due to regeneration of implementation class)
			-- can be added here.
		do
			set_default_cancel_button (close_button)
			set_default_push_button (close_button)
			tip_counter := Preferences.integer_resource_value (preferences.tip_of_day_index, 1)
			if Preferences.boolean_resource_value (Preferences.Show_tip_of_the_day, True) then
				show_tips_button.enable_select
			else
				show_tips_button.disable_select
			end
			tip_label.set_text (all_tips @ tip_counter)
		end

feature {NONE} -- Implementation

	tip_counter: INTEGER
		-- Current tip to be displayed.

	window_shown is
			-- Called by `show_actions' of `gb_tip_of_the_day_dialog'.
		do
			if blocking_window /= Void then
				set_position (blocking_window.x_position + blocking_window.width // 2 - width // 2,
					blocking_window.y_position + blocking_window.height // 2 - height // 2)
			end
		end

	next_tip_selected is
			-- Called by `select_actions' of `next_tip_button'.
		do
			tip_counter := get_next_tip_index (tip_counter)
			tip_label.set_text (all_tips @ tip_counter)
		end
		
	get_next_tip_index (current_index: INTEGER): INTEGER is
			-- `Result' is next valid tip index based on `current_index'.
		do
			Result := current_index + 1
			if result > tip_count then
				Result := 1
			end
		ensure
			Result_valid: Result >= 1 and Result <= tip_count
		end

	close_button_selected is
			-- Called by `select_actions' of `close_button'.
		do
			Preferences.set_boolean_resource (preferences.Show_tip_of_the_day, show_tips_button.is_selected)
			Preferences.set_integer_resource (preferences.tip_of_day_index, get_next_tip_index (tip_counter))
			Preferences.save_resources
			destroy
		end

end -- class GB_TIP_OF_THE_DAY_DIALOG

