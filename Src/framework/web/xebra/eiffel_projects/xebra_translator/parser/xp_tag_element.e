note
	description: "[
		A {XP_TAG_ELEMENT} is generated by parsing a "xeb"-file. {XP_TAG_ELEMENT}s
		are combined to trees.
	]"
	author: "sandro"
	date: "$Date$"
	revision: "$Revision$"

class
	XP_TAG_ELEMENT

create
	make

feature -- Initialization

	make (a_id: STRING; a_class_name: STRING)
			-- `a_class_name': The name of the corresponding TAG-class
		require
			a_id_valid: not a_id.is_empty
			a_class_name_is_valid: not a_class_name.is_empty
		do
			class_name := a_class_name
			id := a_id
			create {ARRAYED_LIST [STRING]} controller_calls.make (1)
			create {ARRAYED_LIST [XP_TAG_ELEMENT]} children.make (3)
			create {HASH_TABLE [STRING, STRING]} parameters.make (3)
			create {HASH_TABLE [STRING, STRING]} dynamic_parameters.make (3)
		end

feature {NONE} -- Access

	class_name: STRING
			-- The name of the corresponding TAG-class

	parameters: HASH_TABLE [STRING, STRING]
			-- The parameters of the tag [value, parameter id]

	dynamic_parameters: HASH_TABLE [STRING, STRING]
			-- Dynamic parameters of the tag [feature name, parameter id]

	children: LIST [XP_TAG_ELEMENT]
			-- All the children of the tag

	controller_calls: LIST [STRING]
			-- All calls to the controller, which should be possible

feature -- Access

	id: STRING
			-- The tag id

	multiline_argument: BOOLEAN assign set_multiline_argument
			-- Are the arguments of the tag multiline and have to be escaped?

	set_multiline_argument (is_multiline: BOOLEAN)
			-- Are the arguments of the tag multiline and have to be escaped?
		do
			multiline_argument := is_multiline
		end

	has_children: BOOLEAN
			-- Are there any children?
		do
			Result := not children.is_empty
		end

	has_attribute (name: STRING): BOOLEAN
		do
			Result := parameters.has_key (name)
		end

	put_controller_call (a_call: STRING)
			-- Add a feature that should be available to servlets
		require
			call_is_not_empty: not a_call.is_empty
		do
			controller_calls.extend (a_call)
		ensure
			call_has_been_added: old controller_calls.count = controller_calls.count - 1
		end

	put_subtag (child: like Current)
			-- Adds a tag to the list of children.
		do
			children.extend (child)
		ensure
			child_has_been_added: old children.count + 1 = children.count
		end

	put_attribute (a_local_part: STRING; a_value: STRING)
			-- Sets the attribute of this tag.
		require
			local_part_is_not_empty: not a_local_part.is_empty
			value_is_not_empty: not a_value.is_empty
		do
			parameters.extend (a_value, a_local_part)
		ensure
			attribute_has_been_added: old parameters.count + 1 = parameters.count
		end

	put_dynamic_attribute (a_local_part: STRING; a_feature_name: STRING)
			-- Sets an attribute that will be a feature call on the controller
		require
			local_part_is_not_empty: not a_local_part.is_empty
			feature_name_is_not_empty: not a_feature_name.is_empty
		do
			dynamic_parameters.extend (a_feature_name, a_local_part)
		ensure
			attribute_has_been_added: old dynamic_parameters.count + 1 = dynamic_parameters.count
		end

	build_tag_tree (a_feature: XEL_FEATURE_ELEMENT)
			-- Adds the needed expressions which build the tree of Current with the correct classes
		do
			internal_build_tag_tree (a_feature, True)
		end

	internal_build_tag_tree (a_feature: XEL_FEATURE_ELEMENT; is_root: BOOLEAN)
			-- Adds the needed expressions which build the tree of Current with the correct classes
		do
			a_feature.append_expression ("create {" + class_name + "} temp.make")
			if is_root then
				a_feature.append_expression ("root_tag := temp")
			else
				a_feature.append_expression ("stack.item.add_to_body (temp)")
			end
			build_attributes (a_feature, parameters, "CONSTANT_ATTRIBUTE")
			build_attributes (a_feature, dynamic_parameters, "VARIABLE_ATTRIBUTE")

			if has_children then
				a_feature.append_expression ("stack.put (temp)")
				from
					children.start
				until
					children.after
				loop
					children.item.internal_build_tag_tree (a_feature, False)
					children.forth
				end
				a_feature.append_expression ("stack.remove")
			end
		end

	build_attributes (a_feature: XEL_FEATURE_ELEMENT; attributes: HASH_TABLE [STRING, STRING]; type: STRING)
			-- Adds expressions which put the right attributes to the tags
		require
			type_is_not_empty: not type.is_empty
		do
			from
				attributes.start
			until
				attributes.after
			loop
				if multiline_argument then
					a_feature.append_expression ("temp.put_attribute(%""
						+ attributes.key_for_iteration + "%", "
						+ "%N%"[%N" + attributes.item_for_iteration + "%N]%")"
					)
				else
					a_feature.append_expression ("temp.put_attribute(%""
						+ attributes.key_for_iteration + "%", "
						+ "%"" + attributes.item_for_iteration + "%")"
					)
				end
				attributes.forth
			end
		end

note
	copyright: "Copyright (c) 1984-2009, Eiffel Software"
	license: "GPL version 2 (see http://www.eiffel.com/licensing/gpl.txt)"
	licensing_options: "http://www.eiffel.com/licensing"
	copying: "[
			This file is part of Eiffel Software's Eiffel Development Environment.
			
			Eiffel Software's Eiffel Development Environment is free
			software; you can redistribute it and/or modify it under
			the terms of the GNU General Public License as published
			by the Free Software Foundation, version 2 of the License
			(available at the URL listed under "license" above).
			
			Eiffel Software's Eiffel Development Environment is
			distributed in the hope that it will be useful, but
			WITHOUT ANY WARRANTY; without even the implied warranty
			of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
			See the GNU General Public License for more details.
			
			You should have received a copy of the GNU General Public
			License along with Eiffel Software's Eiffel Development
			Environment; if not, write to the Free Software Foundation,
			Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
		]"
	source: "[
			Eiffel Software
			5949 Hollister Ave., Goleta, CA 93117 USA
			Telephone 805-685-1006, Fax 805-685-6869
			Website http://www.eiffel.com
			Customer support http://support.eiffel.com
		]"
end
