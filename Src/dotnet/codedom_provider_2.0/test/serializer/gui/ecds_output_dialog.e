indexing
	description: "Objects that represent an EV_DIALOG.%
		%The original version of this class was generated by EiffelBuild."
	date: "$Date$"
	revision: "$Revision$"

class
	ECDS_OUTPUT_DIALOG

inherit
	ECDS_OUTPUT_DIALOG_IMP

	ECDS_OUTPUT_TAGS
		undefine
			copy,
			default_create
		end

feature {NONE} -- Initialization

	user_initialization is
			-- called by `initialize'.
			-- Any custom user initialization that
			-- could not be performed in `initialize',
			-- (due to regeneration of implementation class)
			-- can be added here.
		do
			set_default_push_button (ok_button)
		end

feature -- Basic Operation

	clear is
			-- Clear dialog content.
		do
			output_text.remove_text
		end
	
	write (a_text: STRING) is
			-- Append `a_text' to `output_text'.
			-- `a_text' may contain tags to define formatting style.
			-- See {ECDS_OUTPUT_TAGS} for possible tag values.
		require
			non_void_text: a_text /= Void
		local
			l_index, l_end_index, l_old_index, l_count: INTEGER
			l_tag, l_closing_tag: STRING
		do
			a_text.prune_all ('%R')
			from
				l_index := a_text.index_of (Start_opening_tag, 1)
				l_count := a_text.count
				l_old_index := 1
			until
				l_index = 0 or l_index = l_count
			loop
				output_text.append_text (a_text.substring (l_old_index, l_index - 1))
				l_old_index := l_index
				l_end_index := a_text.index_of (End_opening_tag, l_index + 1)
				if l_end_index > 0 then
					l_tag := a_text.substring (l_index + 1, l_end_index - 1)
					output_tags.search (l_tag)
					if output_tags.found then
						l_closing_tag := closing_tag (l_tag)
						l_index := a_text.substring_index (l_closing_tag, l_end_index + 1)
						if l_index > 0 then
						--	output_text.set_current_format (output_tags.found_item)
							output_text.append_text (a_text.substring (l_end_index + 1, l_index - 1))
						--	output_text.set_current_format (Normal_format)
							l_old_index := l_index + l_closing_tag.count
						end
					end
				end
				l_index := a_text.index_of (Start_opening_tag, l_index + 1)
			end
			output_text.append_text (a_text.substring (l_old_index, a_text.count))
		end

feature {NONE} -- Events

	on_ok is
			-- Called by `select_actions' of `ok_button'.
			-- Hide dialog
		do
			hide
		end

end -- class ECDS_OUTPUT_DIALOG

--+--------------------------------------------------------------------
--| Eiffel CodeDOM Serializer
--| Copyright (C) 2001-2006 Eiffel Software
--| Eiffel Software Confidential
--| All rights reserved. Duplication and distribution prohibited.
--|
--| Eiffel Software
--| 356 Storke Road, Goleta, CA 93117 USA
--| http://www.eiffel.com
--+--------------------------------------------------------------------
